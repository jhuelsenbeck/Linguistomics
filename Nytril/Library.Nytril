using Format, Units, Math, IO;
//======================================================================

with TreeLib {
  GetNodeLabel(node) = node.?Data?.Name;

  VisitNodeTaxa(set, node) {
    if (valid node.?Data)
      set.Add(node.Data);
    VisitNodeTaxa(set, each node);
  }

  GetTaxaLabels(tree) {
    var set = new DictionaryClass(256);
    VisitNodeTaxa(set, tree);
    return (each set).Name;
  }
}
//======================================================================

class TaxonClass {
  var Name,
      Characters,
      Segments,
      NChar;

  Constructor(lang, index) {
    NChar      = 0;
    Name       = lang.GlobalVariableName;
    Segments   = GetSegments(index);
    Characters = (each Segments).SegmentToCharacter;
  }

  Show(maxlength, range) = Span {
    Span {
      Nexus.TaxonFormat;
      Name;
    };
    Space * (maxlength - Name.Length);
    (each Characters[range]).ShowCharacter;
  };

  GetSegments(index) {
    var list = new ListClass(256);
    foreach (var concept in ConceptsList) {
      var ma   = Results.WordConceptArray[EachIndex];
      var word = ma.Cells[index];
      var pad  = ma.MaxLength - word.Segments.Length;
      list.Add(IPA.LeftSegment);
      list.Add(each word.Segments);
      NChar += word.Segments.Length + pad;
      if (pad > 0) {
        foreach (var i in 1..pad)
          list.Add(IPA.GapSegment);
      }
      list.Add(IPA.RightSegment);
    }
    return list;
  }
}
//======================================================================

with Nexus {
  TaxonFormat = {TextColor: Colors.DarkGreen};

  CharacterList = ('A'..'Z' step 1) + ('a'..'z' step 1) + ('0'..'9' step 1);
  EndMarker     = ";";
  Missing       = "?";
  Quote         = "\"";

  Keyword(name) = Span {
    TextColor: Colors.Blue;
    name
  };

  Comment(text) = Span {
    TextColor: Colors.Green;
    "#"
    text
  };

  AddLine(name) = Span {
    Keyword(name);
    End: EndMarker;
  };

  AddValue(name, value) = Span {
    Space;
    Keyword(name);
    "=";
    value;
  };

  AddTaxon(taxon) = Span {
    TaxonFormat;
    Space;
    taxon.Name;
  };

  Scope(name) = TextBlock {
    IndentSpace: 2;
    Begin: Span {
      Keyword("begin");
      Space;
      name;
      EndMarker;
    };
    End: Span {
      Keyword("end");
      EndMarker;
      LineBreak;
    };
  };

  NexusFile = TextBlock {
    Style.MonoFamily;
    Comment("NEXUS");
  };

  Newick(node) = Span {
    if (node.Length > 0) {
      "(";
      Span {
        Separator: ",";
        Newick(each node)
      };
      ")";
    }
    TreeLib.GetNodeLabel(node);
    ":";
    node.BranchLength
  };

  ShowTreeLine(tree) = Span {
    "tree ";
    tree.GlobalVariableName;
    " = ";
    Newick(tree);
    EndMarker
  };

  TreeFormat(taxa) = NexusFile {
    Scope("taxa") {
      AddLine("dimensions") {
        AddValue("ntax", taxa.Length);
      };
      AddLine("taxlabels") {
        Space;
        Span {
          Separator: Space;
          taxa;
        };
      };
    }
  };

  TreeFile(tree) = TreeFormat(TreeLib.GetTaxaLabels(tree)) {
    Scope("trees") {
      ShowTreeLine(tree);
    }
  };

  CharacterFile(taxa) = NexusFile {
    var maxlength = ((each ((each taxa).Name)).Length).Max + 1;
    var range     = ((0..<taxa[0].Characters.Length) / (70 - maxlength));
/*
    Scope("taxa") {
      AddLine("dimensions") {
        AddValue("ntax", taxa.Length);
      };
//      AddLine("taxlabels") {
        AddTaxon(each taxa);
      };
    };
*/
    Scope("data") {
      AddLine("dimensions") {
        AddValue("ntax", taxa.Length);
        AddValue("nchar", taxa[0].NChar);
      };
//      AddLine("taxlabels") {
//        AddTaxon(each taxa);
//      };
      AddLine("format") {
        AddValue("datatype", "STANDARD");
        AddValue("gap", IPA.GapSegment.Ipa);
        AddValue("missing", Missing);
        AddValue("symbols", Span {Quote; CharacterList[Results.UniqueSegments.IndexRange]; Quote});
      };
      Empty;
      Keyword("matrix");
      (each taxa).Show(maxlength, each range);
      EndMarker;
    }
  };
}
//======================================================================

