using Format, Units, Features, Math, Style;
//======================================================================

class FilterClass {
  field        Name;
  var          CircleName;
  DataSetClass Data;
  RuleClass    Rule;

  Constructor(DataSetClass data, RuleClass rule, name) {
    Data       = data;
    Rule       = rule;
    Name       = name;
    CircleName = name.Substitute(name.FindIndex(c => c == Space), LineBreak);
  }

  virtual Matches(SegmentClass segment) = true;

  override GetLayoutDoc = Span {
    Hover: new HoverClass(this, HoverTypes.Tip);
    Name;
  };

  override GetLayoutTip = Frame {
    Width: 2 inches;
    Paragraph {
      ParAlignment: ParAlignments.Center;
      TextHeight: 16 pts;
      BorderB: 1;
      Name;
    };
    SegmentDisplay.ShowFilterSegments(Rule, Data.UniqueSegments, this, ParAlignments.Left);
  };
}
//======================================================================

class PartitionClass: FactClass {
  field          Name;
  SegmentClass[] Segments;
  var            Index,
                 Center,
                 Radius,
                 Angle,
                 Outer;
  FilterClass    Filter;

  Constructor(index, FilterClass filter, SegmentClass[] segments) {
    super.Constructor(filter.Name);
    Name     = filter.Name;
    Index    = index;
    Filter   = filter;
    Segments = segments;
    LinkTypes.Link.Connect(this, segments);
  }

  override GetIcon = MaterialIcons.group_work;

  override GetLayoutTip = Filter.GetLayoutTip;
}
//======================================================================

abstract class RuleClass {
  DataSetClass     Data;
  ListClass        FilterList;
  FilterClass[]    Filters,
                   FiltersPlus;
  PartitionClass[] Partitions;
  SegmentClass[]   PartSegments;

  var              Name,
                   Matrix,
                   Numbers;

  Constructor(DataSetClass data, name) {
    Data       = data;
    Name       = name;
    Numbers    = false;
    FilterList = new ListClass<FilterClass>(128);
  }

  MatchesRule(FilterClass filter, SegmentClass segment) {
    foreach (var f in FiltersPlus) {
      if (f == filter)
        return f.Matches(segment);
      if (f.Matches(segment))
        return false;
    }
    return false;
  }

  virtual GetFilterIndex(SegmentClass segment) {
    var c = 0;
    foreach (var filter in FiltersPlus) {
      if (MatchesRule(filter, segment))
        return c;
      ++c;
    }
    return c;
  }

  void SetSegmentCharacter(SegmentClass segment) {
    if (not Numbers) {
      if (segment.Index < Nexus.CharacterList.Length)
        segment.CharacterLetter = Nexus.CharacterList[segment.Index];
      else
        System.Assert(false, "Too many characters");
    }
  }

  void GetFilters {
    Filters = FilterList.Values;
    FilterList.Add(new FilterClass(Data, this, "Residue"));
    FiltersPlus = FilterList.Values;
  }

  virtual void AssignCharacters(SegmentClass[] segments) {
    var last = Nexus.CharacterList.Length-1;
    foreach (var segment in segments) {
      var index = GetFilterIndex(segment);
      if (index > last)
        index = last;
      SetSegmentCharacter(segment, index);
    }
    GetFilters;
  }

  void AssignPartitions(SegmentClass[] segments) {
    var list = new ListClass<PartitionClass>(segments.Length);
    var i    = 0;
    foreach (var filter in FiltersPlus) {
      var find = GetMatching(segments, filter);
      if (valid find) {
        list.Add(new PartitionClass(i, filter, find));
        foreach (var s in find)
          s.PartitionIndex = i;
        ++i;
      }
    }
    Partitions   = list.Values;
    PartSegments = Data.UniqueSegments.Sort(true, ref ComparePartSegments);
  }

  static ComparePartSegments(SegmentClass s0, SegmentClass s1, options) {
    var i = s1.PartitionIndex - s0.PartitionIndex;
    if (i == 0)
      i = s0.UsedCount - s1.UsedCount;
    return i;
  }

  SegmentClass[] GetMatching(SegmentClass[] segments, FilterClass filter) {
    return segments.FindSlice(segment => MatchesRule(filter, segment));
  }

  ShowPartitions = Block {
    Paragraph {
      TextHeight: 16 pts;
      "Model: ";
      Style.DQuote {Name};
      BorderB: 1;
    };

    foreach (var partition in Partitions) {
      Paragraph {
        TextHeight: 12 pts;
        SpaceBefore: 4 pts;
        TabStops: [0.35 inch];
        LeftIndent: 2.85 inches;
        FirstIndent: -2.85 inches;
        each1;
        ")"
        Tab;
        partition.Filter.Name;
        Tab;
        Span {
          Separator: Space;
          (each partition.Segments).ShowSegment;
        }
      }
    }
  };

  ShowDescription(SegmentClass[] segments) = Block {
    Paragraph {
      TextHeight: 14 pts;
      Name;
    };

    Paragraph {
      Separator: Space;

      var i = 0;
      foreach (var filter in FiltersPlus) {
        var matching = GetMatching(segments, filter);
        if (valid matching) {
          HBox {
            VAlign: VAligns.Center;
            Margin: 2;
            PaddingLR: 1;
            Border: 0.25 pts {Color: 80%};
            Span {
              Style.MonoFamily;
              HAlign: HAligns.Center;
              if (Numbers) {
                i++;
                i;
              }
              else if (i < Nexus.CharacterList.Length)
                Nexus.CharacterList[i++];
              else
                System.Assert(false);
            };
            Space*5;
            Span {
              TextHeight: 18 pts;
              Separator: Space;
              (each matching).ShowSegment;
            };
          }
        }
      }
    }
  };
}
//======================================================================

class SingleFilterClass: FilterClass {
  SegmentClass Segment;

  Constructor(DataSetClass data, RuleClass rule, SegmentClass segment) {
    super.Constructor(data, rule, segment.Ipa);
    Segment = segment;
  }

  override Matches(SegmentClass segment) = segment == Segment;
}
//======================================================================

class FeatureMaskClass: FilterClass {
  var Mask,
      Flags;

  Constructor(DataSetClass data, RuleClass rule, name, mask, flag) {
    super.Constructor(data, rule, name);
    Mask  = mask;
    Flags = flag;
  }

  override Matches(SegmentClass segment) = segment.Features.HasFlagMask(Mask, Flags);
}
//======================================================================

class RootVowelClass: FilterClass {
  Constructor(DataSetClass data, RuleClass rule) {
    super.Constructor(data, rule, "Root vowel");
  }

  override Matches(SegmentClass segment) = segment.Features.HasFlag(Vowel) and valid segment.Root;
}
//======================================================================

class DiphthongFilterClass: FilterClass {
  var Root;

  Constructor(DataSetClass data, RuleClass rule, root) {
    super.Constructor(data, rule, "Root+Diphthongs");
    Root = root;
  }

  override Matches(SegmentClass segment) = segment == Root or (segment.Features.HasFlag(Diphthong) and segment.Root == Root);
}
//======================================================================

class SegmentListClass: FilterClass {
  SegmentClass[] List;

  Constructor(DataSetClass data, RuleClass rule, name, SegmentClass[] list) {
    super.Constructor(data, rule, name);
    List = list;
  }

  override Matches(SegmentClass segment) = List.FindAny(segment);
}
//======================================================================

class VowelListClass: SegmentListClass {
  Constructor(DataSetClass data, RuleClass rule, SegmentClass[] list) {
    super.Constructor(data, rule, list.FirstElement.Ipa, list);
  }

  override Matches(SegmentClass segment) = List.FindAny(segment);
}
//======================================================================

class Partition2Class: RuleClass {
  Constructor(DataSetClass data) {
    super.Constructor(data, "Linguistically Informed");
    Numbers = true;

    FilterList.Add(new VowelListClass(data, this, [IPA.Segments.ay, IPA.Segments.ai]));
    FilterList.Add(new VowelListClass(data, this, [IPA.Segments.e, IPA.Segments.E]));
    FilterList.Add(new VowelListClass(data, this, [IPA.Segments.ah, IPA.Segments.A]));
    FilterList.Add(new VowelListClass(data, this, [IPA.Segments.u, IPA.Segments.U]));
    FilterList.Add(new VowelListClass(data, this, [IPA.Segments.oh, IPA.Segments.O]));
    FilterList.Add(new VowelListClass(data, this, [IPA.Segments.euh, IPA.Segments.LongOpenMidBackRounded]));
    FilterList.Add(new VowelListClass(data, this, [IPA.Segments.eh, IPA.Segments.Eh]));
    FilterList.Add(new VowelListClass(data, this, [IPA.Segments.uu, IPA.Segments.o]));
    FilterList.Add(new FeatureMaskClass(data, this, "Nasal Vowel", Vowel Nasal, Vowel Nasal));
    FilterList.Add(new FeatureMaskClass(data, this, "Vowel", Vowel, Vowel));
    FilterList.Add(new FeatureMaskClass(data, this, "Nasal Consonant", Nasal, Nasal));
    FilterList.Add(new SegmentListClass(data, this, "Non Sylabic Sonorant", [IPA.Segments.l, IPA.Segments.r, IPA.Segments.n, IPA.Segments.m, IPA.Segments.jg, IPA.Segments.w]));
    FilterList.Add(new FeatureMaskClass(data, this, "Consonant", Vowel, NoFeature));
    GetFilters;
    AssignPartitions(data.UniqueSegments);
  }
}
//======================================================================

class PartitionHintClass {
  var            Rate;
  PartitionClass P1,
                 P2;
  RuleClass      Rule;
  DataSetClass   Data;

  Constructor(DataSetClass data, RuleClass rule, rate, PartitionClass p1, PartitionClass p2) {
    Rule = rule;
    Data = data;
    Rate = rate;
    P1   = p1;
    P2   = p2;
  }

  AddGroup(PartitionClass p) = VBox {
    HAlign: HAligns.Center;
    HBox {
      TextBackground: 90%;
      Span {
        p.Filter.Name;
      };
    };
    HBox {
      SegmentDisplay.ShowFilterSegments(Rule, Data.UniqueSegments, p.Filter, ParAlignments.Center);
    };
  };

  override GetLayoutTip = HBox {
    VAlign: VAligns.Top;
    if (P1 != P2)
      AddGroup(P1);
    HBox {
      MarginLR: 5;
      PaddingLR: 5;
      Background: 90%;
      VBox {
        HAlign: HAligns.Center;
        "Transitions";
        Span {
          Bold;
          TextHeight: 14 pts;
          TextDigits: 0;
          Rate;
        }
      };
    };
    AddGroup(P2);
  };
}
//======================================================================

class TransitionClass {
  var       Frequencies,
            Rates;
  RuleClass Rule;

  Constructor(DataSetClass data, RuleClass rule, read=false) {
    Rule = rule;
    if (read)
      ReadFrequencies;
    else
      StaticFrequencies(data.UniqueSegments, data.TransitionCount);
  }

  RandomArray {
    var list = new ListClass(Rule.Partitions.Length);
    foreach (var f in Rule.Partitions)
      list.Add(Math.Random(0..1000));
    return list.Values;
  }

  void ReadFrequencies {
    // This is temporary until we get actual data

    Frequencies = RandomArray;
    var list = new ListClass(Rule.Partitions.Length);
    foreach (var f in Rule.Partitions)
      list.Add(RandomArray);
    Rates = list.Values;
  }

  void StaticFrequencies(SegmentClass[] segments, MatrixClass tcount) {
    var max   = Rule.Partitions.Length;
    var list  = new ListClass(max);
    var flist = new ListClass(max);
    foreach (var p0 in Rule.Partitions) {
      var s0  = Rule.GetMatching(segments, p0.Filter);
      var sum = Math.Sum((each s0).UsedCount);

      flist.Add(sum);

      var l1 = new ListClass(max);
      foreach (var p1 in Rule.Partitions) {
        var s1    = Rule.GetMatching(segments, p1.Filter);
        var count = 0;
        foreach (var s0i in s0) {
          foreach (var s1i in s1) {
            var tl = tcount.GetValue(s0i.Index, s1i.Index);
            if (valid tl)
              count += tl.Length;
          }
        }

        l1.Add(count);
      }
      list.Add(l1.Values);
    }
    Frequencies = flist.Values;
    Rates       = list.Values;
  }
}
//======================================================================

class TranClass {
  var List,
      Percent;

  Constructor(list, percent) {
    List    = list;
    Percent = percent;
  }

  override GetLayoutTip = Frame {
    var first = List.FirstElement;
    Paragraph {
      first.Word1.Padded[first.Index].ShowSegment;
      Tex.To;
      first.Word2.Padded[first.Index].ShowSegment;
    };
    Paragraph {
      List.Length; " transitions";
    };
    Paragraph {
      TextDigits: 1;
      Percent * 100.0; "% of max";
    };

    var count = Math.Min([List.Length, 30]);

    Width: Min([count*3, 6]) inches;

    Paragraph {
      Separator: Space;
      foreach (var p in List[0..count])
        p.ShowDescription(10 pts);
    };

    if (count < List.Length)
      "More...";
  };
}
//======================================================================

class CalcRatesClass {
  DataSetClass    Data;
  TransitionClass Transition;
  RuleClass       Rule;
  var             MaxLine,
                  ArrowGap,
                  GapAngle,
                  LoopRadius,
                  LoopDistance,
                  MaxExtent,
                  MaxRadius,
                  RateScale;

  Constructor(DataSetClass data, RuleClass rule, TransitionClass transition=null) {
    Data       = data;
    Rule       = rule;
    ArrowGap   = 2 pts;
    GapAngle   = 80.0 degrees;
    Transition = transition ?? new TransitionClass(Data, rule);
  }

  void Calibrate(size) {
    var middle = Point(size.Width * 0.5, size.Height * 0.5);

    MaxExtent    = Min([middle.X, middle.Y]);
    MaxRadius    = MaxExtent * 0.13;
    MaxLine      = MaxRadius * 0.3;
    LoopRadius   = MaxRadius * 0.25;
    LoopDistance = LoopRadius * 0.5;


    var fmax = MaxRadius / Sqrt(Real(Max(Transition.Frequencies))); // Proportional to area, so use SQRT of frequency

    RateScale = MaxLine / Max(Max(each Transition.Rates));

    var mult = 2.0 * Graphics.OneEighty / Rule.Partitions.Length;
    foreach (var partition in Rule.Partitions) {
      partition.Angle  = mult * each0 - Graphics.Ninety;
      partition.Radius = Sqrt(Real(Transition.Frequencies[each0])) * fmax;
    }

    var maxradius = 0.0;
    foreach (var partition in Rule.Partitions)
      maxradius = Max([maxradius, partition.Radius + Transition.Rates[each0][each0] * RateScale]);
    maxradius = MaxExtent - maxradius - LoopRadius - LoopDistance - ArrowGap;

    foreach (var partition in Rule.Partitions)
      partition.Center = Graphics.ExtendPoint(middle, maxradius, partition.Angle);
  }

  private MainDiagram(min) = Canvas {
    var size = Size(min, min);

    TransformFit: true;
    Calibrate(size);
    Size: size;

    foreach (var p1 in Rule.Partitions) {
      var pi = each0;
      foreach (var p2 in Rule.Partitions) {
        var  rate = Transition.Rates[pi][each0];
        var  hint = new PartitionHintClass(Data, Rule, rate, p1, p2);
        var  line = new LineStyleClass(rate * RateScale, 0.25, 1);
        var  r    = rate * RateScale / MaxLine;
        var  frac = (100.0 * Math.Max([0.0, 1.0 - r]))%;
        line.LineColor = Color(frac, frac, frac, (50*(r + 1))%);

        if (p1 == p2) {
          var c = Graphics.ExtendPoint(p1.Center, p1.Radius + LoopDistance - line.LineSize*0.5, p1.Angle);
          Graphics.CircleLine(line, c, LoopRadius, p1.Angle - Graphics.OneEighty, GapAngle) {
            Hover: new HoverClass(hint, HoverTypes.Tip);
          };
        }
        else if (each0 < pi) {
          var angle = Graphics.GetAngle(p1.Center, p2.Center);
          var c1    = Graphics.Adjust(p1.Center, p1.Radius - ArrowGap, angle);
          var c2    = Graphics.Adjust(p2.Center, p2.Radius - ArrowGap, angle + Graphics.OneEighty);

          Graphics.Line(line, c1, c2) {
            Hover: new HoverClass(hint, HoverTypes.Tip);
          };
        }
      }
    }

    foreach (var p1 in Rule.Partitions) {
      var pos   = p1.Center - p1.Radius;
      var fsize = p1.Radius*2;

      Figure {
        Fill: ChartColor(each0);
        Stroke: 0.5 pt;
        Ellipse(Rect(pos, fsize));
        Hover: new HoverClass(p1, HoverTypes.Tip);
      };

      var c = LayoutSize(TestText(p1)).Width;
      CircleText(p1, pos, fsize, Math.Min([12 pts, p1.Radius * 20 / c]));
    }
  };

  TestText(PartitionClass p) = Paragraph {
    TextHeight: 10 pts;
    p.Filter.CircleName;
  };

  CircleText(PartitionClass p, pos, fsize, textheight) = HBox {
    Position: pos;
    Height: fsize;
    Frame {
      VAlignment: VAligns.Center;
      Width: fsize;
      TextHeight: textheight;
      ParAlignment: ParAlignments.Center;
      p.Filter.CircleName;
    };
  };

  PartitionDiagram(SizeClass wholesize) = VBox {
    Width: wholesize.Width;
    HAlign: HAligns.Center;

    HBox {
      VAlign: VAligns.Center;

      VBox {
        Table {
          TextHeight: 10 pts;
          Columns: [20 pts, 1 inch];

          foreach (var partition in Rule.Partitions) {
            Row {
              Span {
                each1;
                ". ";
              };
              Paragraph {
                LeftIndent: 15 pts;
                FirstIndent: -15 pts;
                Figure {
                  Fill: ChartColor(each0);
                  Ellipse(Rect(0, 10 pts));
                };
                partition.Filter;
              }
            }
          }
        };
      };

      MainDiagram(Min([wholesize.Width, wholesize.Height]));
    };
  };

  GetColor(SegmentClass s) = ChartColor(s.PartitionIndex);// * 30%;

  TranLink(i0, i1) = "Tran{0}:{1}"(i0, i1);

  TranPar(list) = Block {
    var first = list.FirstElement;
    Paragraph {
      var s0 = first.Word1.Padded[first.Index];
      s0.ShowSegment;

      Tex.To;
      var s1 = first.Word2.Padded[first.Index];
      s1.ShowSegment;

      LocationMark: TranLink(s0.Index, s1.Index);
      KeepWithNext: true;
    };

    Paragraph {
      SpaceBefore: 4 pts;
      SpaceAfter: 6 pts;
      Separator: Space;
      foreach (var p in list)
        p.ShowDescription(9 pts);
    }
  };

  static CompareLength(l1, l2, options) = l2.Length - l1.Length;

  GetSorted {
    var all      = new ListClass(128);
    var t        = Data.TransitionCount;
    var segments = Rule.PartSegments;

    foreach (var s0 in segments) {
      foreach (var s1 in segments) {
        if (s0.Index != s1.Index) {
          var tlist = t.GetValue(s0.Index, s1.Index);
          if (valid tlist and tlist.Length < Info.LinkMin)
            all.Add(tlist);
        }
      }
    }

    return all.Sort(true, ref CompareLength);
  }

  PartitionBlock = Block {
    PartitionBox(Size(DocMetrics.PageContent.Width, DocMetrics.PageContent.Height * 0.8));

/*
    PageBreak;
    Block {
      TextHeight: 9 pts;
      TranPar(each GetSorted);
    }
*/
  };

  PartitionBox(SizeClass framesize) = Paragraph {
    var segments = Rule.PartSegments;
    var grid     = 0.125 pts;
    var gcolor   = 65%;
    var border   = 0.5 pts;
    var size     = framesize - border*2;
    var d        = size / (segments.Length+1);
    Canvas {
      TextHeight: d.Height * 0.6;
      Border: border;

      var t   = Data.TransitionCount;
      var max = 1.0 / Data.TransitionMax;
      var y   = d.Height;
      foreach (var s0 in segments) {
        var x = d.Width;
        foreach (var s1 in segments) {
          if (s0.Index != s1.Index) {
            var tlist = t.GetValue(s0.Index, s1.Index);
            if (exists tlist) {
              Canvas {
                HAlign: HAligns.Center;
                VAlign: VAligns.Center;
                X: x;
                Y: y;
                Size: d;

                var count = 0;
                foreach (var e in tlist) {
                  var c0 = e.Word1.Padded[e.Index];
                  var c1 = e.Word2.Padded[e.Index];
                  if (c0.Features.HasFlag(Vowel) != c1.Features.HasFlag(Vowel))
                    ++count;
                }

                if (count > 0) {
                  var f = Integer(255.0 * (1.0 - count / tlist.Length));
                  if (f < 255)
                    Background: Color(f, 255, f);
                }
                else {
                  var f = Integer((1.0 - tlist.Length * max) * 255.0);
                  if (f < 255)
                    Background: Color(255, f, f);
                }

                Span {
                  tlist.Length;
                  Hover: new HoverClass(new TranClass(tlist, tlist.Length*max), HoverTypes.Tip);
                }
              };
            }
          }
          x += d.Width;
        }
        y += d.Height;
      }

      y = d.Height;
      var p = 0;
      foreach (var s in segments) {
        Canvas {
          Size: d;
          Background: GetColor(s);
          Width: d.Width;
          HAlign: HAligns.Center;
          Y: y;
          s.ShowSegment;
        };
        if (p != s.PartitionIndex) {
          Canvas {
            Y: y;
            Size: Size(size.Width, grid);
            Background: gcolor;
          };
          p = s.PartitionIndex;
        }
        y += d.Height;
      }

      Canvas {
        Height: d.Height;
        Width: size.Width;

        var x = d.Width;
        p = 0;
        foreach (var s in segments) {
          Canvas {
            Size: d;
            Background: GetColor(s);
            HAlign: HAligns.Center;
            X : x;
            s.ShowSegment;
          };
          if (p != s.PartitionIndex) {
            Canvas {
              X: x;
              Size: Size(grid, size.Height);
              Background: gcolor;
            };
            p = s.PartitionIndex;
          }
          x += d.Width;
        }
      };
    };
  };
}
//======================================================================

