using Format, Units, Math, IO;

include "English";
include "Spanish";
include "Library";
include "Languages";
include "LanguageTree";
include "IPA";
include "Rules";
include "RevBayes";
include "Style";
include "References";
include "Concepts";
include "Formulas";
include "Methods";
include "Slides";
include "Tables";
include "../../nytril/deploy/citations/citations";
//======================================================================

Main.Documents = new DocumentViewClass(null) {
  new PaperViewClass;
  new ExperimentViewClass;
  new DiagnosticsViewClass;
  new JohnPresentationClass;
  new DavidPresentationClass;
};

void Main.Run {
  Results.Calculate;
}
//======================================================================

// No spaces in the name below!
// RevBayes has a problem with folder names that contain spaces...

Results = new ResultsClass("Run1");

ViewInfo = new ViewInfoClass {
  Title: Info.Title;
//  Description: "Test Description";
//  Comment: "Test Comment";
//  Subject: "Test Subject";
//  Keywords: "Test Keywords";
};

DocMetadata = {
  LinkTypes.ViewInfo -> ViewInfo;
  LinkTypes.ViewAuthor -> Authors;
  LinkTypes.ViewReference ->References;
  RefStyle: new ChicagoStyleClass;
};
//======================================================================

with Info {
  IncludeVerbs = true;
  Generations  = 1000;
  ImageFolder  = Folders.Source Folder("Images");
  Journal      = "Transactions of the Beysian Society";
  Publisher    = "The Baysian Society";
  Title        = Lang.SimulatedSegments;
}
//=====================================================================

with ExampleConcept {
  Cognate            = FindSlice(Results.UsedCognates, c => c.Concept == Concepts.Star, 1)[0];
  GetTaxon(language) = Math.FindSlice(Results.Taxa, t => t.Language == language)[0];

  TaxonIndex     = 0;
  Taxon          = Results.Taxa[TaxonIndex];
  Callout        = Results.Taxa[3];
  Name           = ToLower(Cognate.Name);
  ExLanguages    = [Languages.Romanian, Languages.Catalan, Languages.Portuguese, Languages.French, Languages.Friulian];
  TaxonList      = GetTaxon(each ExLanguages);
  LangNames      = (each TaxonList).Language;
  CalloutSegment = FirstLetter(Callout);
  LangWord       = Span {(each Cognate.Words[TaxonIndex].Segments).Ipa};

  FirstLetter(taxon) = Cognate.Words[taxon.Index].Segments[0].ShowSegment;
  FirstLetters = FirstLetter(each TaxonList);
}
//======================================================================

class PaperViewClass: BaseViewClass {
  var Appendices;

  Constructor {
    super.Constructor("White Paper", "paper", ConfigurationClass.DocumentTypes.Normal);

    Appendices = [
      new AppendixClass("Program Output", Results.ShowLanguageTrees),
      new AppendixClass(Lang.CharacterRules, Results.CharacterRule.ShowDescription),
      new AppendixClass(Lang.PartitionRules, Results.PartitionRule.ShowPartitions),
      new AppendixClass(Lang.ConceptsUsed, AllWords.ShowTable),
    ];
  }

  override GetDocument = Style.MainDocument {
    DocMetadata;

    Style.PageSection {
      Header: Style.NormalHeader(Info.Journal) {
        Distance: 0.125 inches;
        MarginT: 0.125 inch;
        Even: Style.NormalHeader(Info.Journal);
        First: Block;
      };

      Paragraph {
        ParAlignment: ParAlignments.Center;
        Logo;
      };
      Style.Title(Info.Title);
      Style.AuthorList(Authors);

      Style.HeaderCentered(Abstract.Title);
      Abstract.Body;

      Style.ShowContent(each Content);
      Style.ShowAuthors(Authors);

      Style.HeaderCentered(Lang.Appendices);
      Style.AppendixRow(each Appendices);
    };
    Style.ShowAppendix(each Appendices);
    PageBreak;
    Bibliography;
  };
}
//======================================================================

class ExperimentViewClass: BaseViewClass {
  var Appendices;

  Constructor {
    super.Constructor("Experiment", "experiment", ConfigurationClass.DocumentTypes.Normal);

    Appendices = [
//      new AppendixClass("TKF Configuration", Results.ConfigFile),
      new AppendixClass(Lang.CharacterRules, Results.CharacterRule.ShowDescription),
      new AppendixClass(Lang.PartitionRules, Results.PartitionRule.ShowPartitions),
      new AppendixClass(Lang.ConceptsUsed, AllWords.ShowTable),
      new AppendixClass(Lang.Results, Results.ShowLanguageTrees),
      new AppendixClass("Statistics", Results.ShowStats),
    ];
  }

  override GetDocument = Style.MainDocument {
    DocMetadata;

    Style.PageSection {
      Style.Title(Info.Title);
      Style.HeaderCentered(Lang.Appendices);
      Style.AppendixRow(each Appendices);
    };
    Style.ShowAppendix(each Appendices);
  };
}
//======================================================================

ShowSymbols = Block {
  (each Defs).Appendix;
};

class DiagnosticsViewClass: BaseViewClass {
  var Appendices;

  Constructor {
    super.Constructor("Diagnostics", "diag", ConfigurationClass.DocumentTypes.Normal);

    Appendices = [
//      new AppendixClass("Character File", Results.CharacterFile(2)),
      new AppendixClass("RevBayes Source File", Results.RevBayesSource),
      new AppendixClass("Segments used in the word list", DisplayCharacters.ShowTable),
      new AppendixClass("Word Lists by Language", DisplayWords.ShowTable),
      new AppendixClass("Segment Groups", MatchingConsonants.ShowTables),
  //    new AppendixClass("Euler Feature Diagram", IPA.FeatureChart),
      new AppendixClass(Lang.IPAFullName, IPA.SegmentTable),
//      new AppendixClass("Feature Tree", SegmentTree.ShowTree),
//      new AppendixClass(Lang.Diacritics, MatchingDiacritics.ShowTable),
    //  new AppendixClass(Lang.NytrilSourceCode, Style.SourceFile(each System.SourceList)),
      new AppendixClass(Lang.Languages, Results.ShowLanguageDetails),
      new AppendixClass("Symbols", ShowSymbols),
    ];
  }

  override GetDocument = Style.MainDocument {
    DocMetadata;
    Style.ShowAppendix(each Appendices);
  };
}
//======================================================================



