using Format, Units, Math, IO;

include "English";
include "Spanish";
include "Library";
include "Languages";
include "LanguageTree";
include "IPA";
include "Rules";
include "Style";
include "References";
include "Concepts";
include "Methods";
include "Tables";
include "RevBayes";
//======================================================================

// No spaces in the name below!
// RevBayes has a problem with folder names that contain spaces...

ShowOutput = "Run2";

//======================================================================

UsedConcepts = [
  Concepts.Sun,
  Concepts.Moon,
  Concepts.Star,
  Concepts.Water,
  Concepts.Rain,
  Concepts.River,
  Concepts.Lake,
  Concepts.Sea,
  Concepts.Salt,
  Concepts.Stone,
  Concepts.Sand,
];
//======================================================================

with Info {
  Generations = 1000;

  PaperTitle  = "White Paper";
  Journal     = "Transactions of the Beysian Society";
  Publisher   = "The Baysian Society";
  Title       = Lang.SimulatedSegments;
  PriorTree   = GetTreeNodes(LanguageBranches.Romance);

  AuthorList = Span {
    Separator: ", ";
    LastSeparator: " {0} "(Lang.And);
    Style.Author(each Authors);
  };
}
//======================================================================

Main.Configurations = [
  new ConfigurationClass("Paper") {
    Language: ConfigurationClass.Languages.English;
    PaperSystem: ConfigurationClass.PaperSystems.US;
    DocumentType: ConfigurationClass.DocumentTypes.Normal;
  },

  new ConfigurationClass("Presentation") {
    Language: ConfigurationClass.Languages.English;
    PaperSystem: ConfigurationClass.PaperSystems.US;
    DocumentType: ConfigurationClass.DocumentTypes.SlideShow;
  },
];

with Main if (DocumentType == DocumentTypes.Normal) {
  Documents = new DocumentViewClass(null) {
    new PaperViewClass(ShowOutput);
    new DiagnosticsViewClass(ShowOutput);
  };
}

with Main if (DocumentType == DocumentTypes.SlideShow) {
  Documents = new DocumentViewClass(null) {
    new JohnPresentationClass;
    new DavidPresentationClass;
  };
}

void Main.Run {
  new ComputeClass(ShowOutput).Calculate;
}
//======================================================================

class PaperViewClass: BaseViewClass {
  Constructor {
    super.Constructor("White Paper");
  }

  GetAppendices = [
    new AppendixClass("Options File", Compute.GetOptionsSource),
    new AppendixClass("Program Output", Compute.ShowLanguageTrees),
  ];

  override GetContent = {
    var appendices = GetAppendices;
    Style.PageSection {
      Header: Style.NormalHeader(Info.Journal) {
        Distance: 0.125 inches;
        MarginT: 0.125 inch;
        Even: Style.NormalHeader(Info.Journal);
        First: Block;
      };

      Paragraph {
        ParAlignment: ParAlignments.Center;
        Logo;
      };
      Style.Title(Info.Title);
      Paragraph {
        ParAlignment: ParAlignments.Center;
        Info.AuthorList;
      };

      Style.HeaderCentered(Abstract.Title);
      Abstract.Body;

      Style.ShowContent(each Content);
      Style.ShowAuthors(Authors);

      Style.HeaderCentered(Lang.Appendices);
      Style.AppendixRow(each appendices);

      Style.ShowReferences(References);
    };
    Style.ShowAppendix(each appendices);
  };
}
//======================================================================

class SlideClass {
  var Title,
      Body;

  Constructor(title, body) {
    Body  = body;
    Title = title;
  }

  GetBody = Block {
    Body;
  };
}
//======================================================================

class PresentationViewClass: BaseViewClass {
  var Slides;

  Constructor(title) {
    super.Constructor(title);
    Slides = new ListClass(64);
  }

  void NewSlide(slide) {
    Slides.Add(slide);
  }

  override GetContent = {
    Section {
      Paragraph {
        TextHeight: 40 pts;
        SpaceBefore: 1.5 inches;
        LeftIndent: 1 inch;
        Info.Title;
      };
      Paragraph {
        TextHeight: 14 pts;
        SpaceBefore: 1.5 inches;
        LeftIndent: 1 inch;
        Info.AuthorList;
      };
    };

    Section {
      SectionBreak: SectionBreaks.NextPage;
      foreach (var slide in Slides)
        AddSlide(slide.Title, EachIndex, slide.GetBody);
    };
  };
}
//======================================================================

class JohnPresentationClass: PresentationViewClass {
  Constructor {
    super.Constructor("John's Presentation");

    NewSlide(new SlideClass(Content.Introduction.Title, Content.Introduction.Body));

    NewSlide(new SlideClass(Abstract.Title, Abstract.Body));

    NewSlide(new SlideClass(Content.Methods.Title, Content.Methods.Body));

    NewSlide(new SlideClass(Lang.ConceptsUsed, AllWords.ShowTable));

    NewSlide(new SlideClass(Lang.TransitionRules, Compute.ShowTransitionRules));

    NewSlide(new SlideClass("Example Tree", Style.SlideFigure(Figures.ExampleTree)));

    NewSlide(new SlideClass("Results", Style.ReadTree(Lang.MCC, Compute.OutFolder Compute.GammaMCC)));

    NewSlide(new SlideClass("Questions", Style.Questions));
  }
}
//======================================================================

class DavidPresentationClass: PresentationViewClass {
  Constructor {
    super.Constructor("David's Presentation");
    NewSlide(new SlideClass(Content.Introduction.Title, Content.Introduction.Body));

    NewSlide(new SlideClass(Abstract.Title, Abstract.Body));

    NewSlide(new SlideClass(Content.Methods.Title, Content.Methods.Body));

    NewSlide(new SlideClass(Lang.ConceptsUsed, AllWords.ShowTable));

    NewSlide(new SlideClass(Lang.TransitionRules, Compute.ShowTransitionRules));

    NewSlide(new SlideClass("Example Tree", Style.SlideFigure(Figures.ExampleTree)));

    NewSlide(new SlideClass("Results", Style.ReadTree(Lang.MCC, Compute.OutFolder Compute.GammaMCC)));

    NewSlide(new SlideClass("Questions", Style.Questions));
  }
}
//======================================================================

class DiagnosticsViewClass: BaseViewClass {
  Constructor {
    super.Constructor("Diagnostics");
  }

  GetAppendices = [
    new AppendixClass(Lang.TransitionRules, Compute.ShowTransitionRules),
    new AppendixClass("Transition Rule File", Compute.TransitionRuleFile),
    new AppendixClass("RevBayes Source File", Compute.RevBayesSource),
//    new AppendixClass("Character File", Nexus.CharacterFile(2)),
    new AppendixClass("TKF Input Files", Nexus.TKFFiles),
    new AppendixClass(Lang.ConceptsUsed, AllWords.ShowTable),
    new AppendixClass("Segments used in the word list", DisplayCharacters.ShowTable),
    new AppendixClass("Word Lists by Language", DisplayWords.ShowTable),
    new AppendixClass("Segment Groups", MatchingConsonants.ShowTables),
    new AppendixClass("Euler Feature Diagram", IPA.FeatureChart),
    new AppendixClass(Lang.IPAFullName, IPA.SegmentTable),
    new AppendixClass("Feature Tree", SegmentTree.ShowTree),
    new AppendixClass(Lang.Diacritics, MatchingDiacritics.ShowTable),
  //  new AppendixClass(Lang.NytrilSourceCode, Style.SourceFile(each System.SourceList)),
  ];

  override GetContent = {
    Style.ShowAppendix(each GetAppendices);
  };
}

//======================================================================



