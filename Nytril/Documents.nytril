using Format, Units, Style, LinkTypes;

Main.MathFamily   = TextFamilies.LinuxLibertineO;
Main.SymbolFamily = TextFamilies.CMUClassicalSerif;

//======================================================================

abstract class BaseDocumentClass: DocumentClass {
  Constructor(GuidClass id, name, type=DocumentTypes.Normal) {
    super.Constructor(id, name, type);
    Margin = new(0.75 inches, 0.9 inches, 0.75 inches, 1 inches);
    Title  = "Understanding Language Evolution Using an Event-Based Model";
  }

  ShowContent(content) = Block {
    Header2 {
      content.Title;
    };

    content.Body(Extent.Size);
  };

  abstract GetContent;

  override GetDocument = Document {
    TextFamily: TextFamilies.TimesNewRoman;
    TextHeight: 9 pts;

    AuthorLink to Authors;
    Link to References;
    RefStyle: new NatureStyleClass;

    GetContent;
  };
}

//======================================================================

class SubmissionViewClass: BaseDocumentClass {
  Constructor {
    super.Constructor(#670da93e-0b8c-46f0-8ab2-01de4ea8ebdb#, "Paper");
  }

  FooterBlock = Block {
    Paragraph {
      ParAlignment: ParAlignments.Right;
      PageNumber; "/"; PageCount;
    }
  };

  override GetContent = Block {
    PageSection {
      Footer: FooterBlock {
        Even: FooterBlock;
        First: Block;
      };

      MainTitle {
        Title;
      };

      ShowAuthorList(Authors);

      Header1 {
        Abstract.Title
      };
      Abstract.Body(Extent.Size);
      ShowContent(PaperSections.Introduction);
      Header1 {
        Lang.Results
      };

      ShowContent(PaperSections.WordTransformation);
      ShowContent(PaperSections.WordSegmentRelationships);
      ShowContent(PaperSections.RomanceLanguagePhylogeny);
      ShowContent(PaperSections.Discussion);
      ShowContent(PaperSections.MethodsSummary);
      ShowContent(PaperSections.DataSummary);
      ShowContent(PaperSections.ModelInference);
      ShowContent(PaperSections.DataCuration);
      ShowContent(PaperSections.Acknowledgements);
      ShowContent(PaperSections.Contributions);
    };

    ShowBibliography;
    PageSection {
      ShowFigureDoc(Extent.Size, Figures.TransitionRates);
    }
  };
}
//======================================================================

class SupplimentalViewClass: BaseDocumentClass {
  Constructor {
    super.Constructor(#9082a441-cf06-4a95-b484-2cb5f5484c59#, "Supplemental Material");
  }

  HeaderBlock = Block {
    Span {
      TextCase: TextCases.AllUpper;
      Title;
    }
  };

  override GetContent = Block {
    var appendices = [
      new AppendixClass(Lang.PartitionRules, Experiment.Rule.ShowPartitions),
//      new AppendixClass(Lang.FunctionalLoad, Experiment.ShowFunctionalLoad),
      new AppendixClass("Experimental Runs", Experiment.ShowExperiment),
    ];

    PageSection {
      Header: HeaderBlock {
        Distance: 0.125 inches;
        MarginT: 0.125 inch;
        Even: HeaderBlock;
        First: Block;
      };

      MainTitle {
        Title;
      };
      ShowAuthorList(Authors);

      ShowContent(PaperSections.Introduction);
      ShowContent(PaperSections.Methods);
      ShowContent(PaperSections.Conclusion);
    };

    ShowBibliography;
    ShowAppendix(each appendices);
  };
}
//======================================================================

class AlignmentViewClass: BaseDocumentClass {
  Constructor {
    super.Constructor(#a14658e4-26f7-4eab-87b7-4f486035103b#, "Alignments");
  }

  override GetContent = Block {
    var chart = new AlignmentChartClass(MainResults);
    chart.ShowChart(Extent.Size);
    PageBreak;
    chart.ShowAll;
  };
}
//======================================================================



