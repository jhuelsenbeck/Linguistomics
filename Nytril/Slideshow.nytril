using Format, Units, Math, LinkTypes;

//======================================================================

class ShowDesignClass {
  var SlideFooter,
      SlideHeader,
      SlidePage,
      SlideContent,
      HeaderGap,
      SlideMargin,
      ItemTextHeight,
      BulletIndent,
      BulletSize,
      BulletDone,
      BulletCurrent,
      ItemsIndent,
      TotalIndent,
      ItemSpacer,
      TitleHeight,
      BodyHeight;


  Constructor(header, footer) {
    HeaderGap      = 12 pts;
    SlideHeader    = header;
    SlideFooter    = footer;
    SlidePage      = DocMetrics.DocumentSize;
    SlideMargin    = Margin(DocMetrics.PageMargin.Left, SlideHeader + HeaderGap, DocMetrics.PageMargin.Right, SlideFooter);
    SlideContent   = Size(DocMetrics.PageContent.Width, DocMetrics.DocumentSize.Height - header - footer - HeaderGap*2);
    ItemTextHeight = 20 pts;
    BulletIndent   = 0.25 inches;
    BulletDone     = Tex.blacksquare;
    BulletCurrent  = Tex.blacktriangleright;
    ItemsIndent    = 1 inch;
    TotalIndent    = ItemsIndent + BulletIndent;
    ItemSpacer     = 10 pts;
    TitleHeight    = 30 pts;
    BodyHeight     = 18 pts;

  }

  virtual HeaderFooter(ShowClass show, title, page) = Block {
    Paragraph {
      PageBreakBefore: true;
    };
  };

  FixedFrame(x, y, width, height) = Frame {
    HRelative: HRelatives.Page;
    VRelative: VRelatives.Page;
    Wrapping: Wrappings.BehindText;
    X: x;
    Y: y;
    Width: width;
    Height: height;
  };
}

SlideDesigns.Plain = new ShowDesignClass(0, 0);

//======================================================================

class ProgressDesignClass: ShowDesignClass {
  Constructor {
    super.Constructor(1.5 inches, 0.36 inches);
  }

  override HeaderFooter(ShowClass show, title, page) = Block {
    Paragraph {
      PageBreakBefore: true;

      FixedFrame(0, 0, SlidePage.Width, SlideHeader) {
        HeaderBackground(show, title, SlidePage.Width, SlideHeader, page);
      };
      FixedFrame(0, SlidePage.Height - SlideFooter, SlidePage.Width, SlideFooter) {
        FooterBackground(title, SlidePage.Width, SlideFooter, page);
      };
    };
  };

  HeaderBackground(ShowClass show, title, width, height, page) = Block {
    Paragraph {
      HBox {
        MarginLR: 24 pts;
        MarginT: 12 pts;
        MarginB: 12 pts;
        Separation: 24 pts;

        var index = 0;
        foreach (var group in show.Groups) {
          VBox {
            TextHeight: 14 pts;
            group.Title;
            HBox {
              TextHeight: 18 pts;
        //      Separation: 3 pts;
              foreach (var item in group.Items) {
                if (index+1 < item.Index)
                  Tex.blackcirc;
                else
                  Tex.circ;
                ++index;
              }
            };
          };
        }
      }
    };
    Paragraph {
      SpaceBefore: 20 pts;
      TextHeight: 30 pts;
      LeftIndent: 24 pts;
      title;
    };
  };

  FooterBackground(title, width, height, page) = Paragraph {
    RightIndent: 10 pts;
    ParAlignment: ParAlignments.Right;
    TextHeight: 16 pts;
    page+1;
    " / ";
    PageCount;
  };
}
//======================================================================

class FancyDesignClass: ShowDesignClass {
  Constructor {
    super.Constructor(0.75 inches, 0.5 inches);
  }

  override HeaderFooter(ShowClass show, title, page) = Block {
    Paragraph {
      PageBreakBefore: true;

      if (page > 0) {
        FixedFrame(0, 0, SlidePage.Width, SlideHeader) {
          HeaderBackground(title, SlidePage.Width, SlideHeader)
        };
        FixedFrame(0, SlidePage.Height -SlideFooter, SlidePage.Width, SlideFooter) {
          FooterBackground(title, SlidePage.Width, SlideFooter, page);
        };
      }
    };
  };

  Design(width, height, right, bar) = Canvas {
    Height: height;

    var x0 = 0;
    var y0 = 0;
    var t  = 0.10 height;
    var x1 = 0.80 width;
    var x2 = 0.90 width;
    var p1 = Point((x2 + x1)*0.5, height * (right ? 0.25 : 0.75));

    Figure {
      Fill: bar;
      FigurePath {
        Closed: true;
        LineTo(x0, t);
        LineTo(x1, t);
        QuadTo(p1, Point(x2, height));
        LineTo(x2, height);
        LineTo(x0, height);
      }
    };

    Figure {
      Fill: Style.BarLine;
      FigurePath {
        Closed: true;
        MoveTo(x0, y0);
        LineTo(x1, y0);
        QuadTo(p1, Point(x2, height-t));
        LineTo(width, height-t);
        LineTo(width, height);
        LineTo(x2, height);
        QuadTo(p1, Point(x1, t));
        LineTo(x0, t);
      }
    };
  };

  HeaderBackground(title, width, height) = Design(width, height, true, Style.HeaderLeftBar) {
    Frame {
      Width: 0.75 width;
      VAlignment: VAligns.Center;
      X: DocMetrics.PageMargin.Left;
      Paragraph {
        TextHeight: 30 pts;
        TextColor: Colors.White;
        title;
      };
    };
  };

  FooterBackground(title, width, height, page) = Design(width, height, false, Style.LeftBar) {
    Frame {
      VAlignment: VAligns.Center;
      X: DocMetrics.PageMargin.Left;
      Width: DocMetrics.PageContent.Width;
      Paragraph {
        TabStops: [(DocMetrics.PageContent.Width) {Type: TabTypes.Right}];
        TextHeight: 16 pts;
        Span {
          TextColor: 75%;
          title
        };
        Tab;
        Span {
          TextColor: Style.HeaderBar1;
          page;
        }
      }
    }
  };
}
//======================================================================

class SlideClass {
  var             Title;
  ShowClass       Show;
  ShowDesignClass Design;

  Constructor(ShowClass show, title) {
    Title  = title;
    Show   = show;
    Design = show.Design;
  }

  virtual ShowBody = null;

  virtual AddSlidePage(page) = Block {
    Design.HeaderFooter(Show, Title, page);
    ShowBody;
  };
}

//======================================================================

class TitleSlideClass: SlideClass {
  var Items;

  Constructor(ShowClass show, variable) {
    super.Constructor(show, variable.Title);
    Items = new ListClass(32, SlideItemClass);
  }

  override AddSlidePage(page) = Block {
    Design.HeaderFooter(Show, null, page++);
    Paragraph {
      ParAlignment:ParAlignments.Center;
      SpaceBefore: 1 inch;
      Style.MainFamily;
      TextHeight: Design.TitleHeight;
      Title;
    };
  };
}

//======================================================================

class IntroSlideClass: SlideClass {
  static NameHeight  = 18 pts;
  static TitleHeight = 32 pts;
  static NameGap     = 10 pts;

  Constructor(ShowClass show, title) {
    super.Constructor(show, title);
  }

  override AddSlidePage(page) = Block {
    Design.HeaderFooter(Show, null, page++);
    ShowBody;
  };

  override ShowBody = Block {
    var gap = (Design.SlideContent.Height - TitleHeight*2 - (NameGap+NameHeight)*(Show.AuthorList.Length+1)) * 0.25;

    ParAlignment: ParAlignments.Center;
    TextHeight: NameHeight;

    Paragraph {
      TextHeight: TitleHeight;
      Title;
    };

    foreach (var author in Show.AuthorList) {
      Paragraph {
        SpaceBefore: EachIndex > 0 ? NameGap : gap;
        author.First;
        Space;
        author.Last;
      };

      if (Show.AuthorList.Length == 1) {
        var department = author.FindFirstFact(DepartmentLink);
        if (valid department) {
          Paragraph {
            department.Name;
          };
          var org = department.FindFirstFact(DepartmentLink);
          if (valid org) {
            Paragraph {
              org.Name;
            };
          }
        }
        Paragraph {
          var email = author.FindFirstFact(WorkEmailLink);
          if (valid email)
            email.Value;
        };
      }
    }

    Paragraph {
      SpaceBefore: gap;
      TextFormat: "dd MMM yyyy";
      System.Now;
    };
  };
}
//======================================================================

class EndSlideClass: SlideClass {
  Constructor(ShowClass show) {
    super.Constructor(show, Lang.Questions);
  }

  override ShowBody = Block {
    Paragraph {
      var h = 150 pts;
      ParAlignment: ParAlignments.Center;
      SpaceBefore: (Design.SlideContent.Height - h) * 0.5;
      Figure {
        TextHeight: h;
        Fill: ((80%)..0%) {Vertical: true};
        "?";
      }
    };
  };
}

//======================================================================

class SingleSlideClass: SlideClass {
  var Variable;

  Constructor(ShowClass show, variable) {
    super.Constructor(show, variable.Title);
    Variable = variable;
  }

  override ShowBody = Block {
    Variable.Body(Show);
  };
}

//======================================================================

class SlideItemClass {
  var Height,
      Content,
      Number,
      Index;

   Constructor(ShowClass show, item, number) {
     Content = Frame {
       Style.MainStyle;
       Width: show.Design.SlideContent.Width - show.Design.TotalIndent;
       var text = item.?Text;
       if (exists text) {
         Paragraph {
           TextHeight: show.Design.ItemTextHeight;
           text;
         };
       }
       else
          item.Body(show);
     };
     Height = LayoutSize(Content).Height;
     Number = number;
     Index  = show.SlideIndex;
   }
}

//======================================================================

class SlideGroupClass {
  var Items;

  Constructor {
    Items = new ListClass(8, SlideItemClass);
  }
}

//======================================================================

class FigureSlideClass: SlideClass {
  var Figure;

  Constructor(ShowClass show, TitleSlideClass main, topic) {
    super.Constructor(show, topic.?Title ?? topic.Figure.Caption(show.Experiment));
    Figure = topic.Figure;
  }

  override AddSlidePage(page) = Block {
    Design.HeaderFooter(Show, Title, page);
    Paragraph {
      ParAlignment: ParAlignments.Center;
      Frame {
        Figure.Body(Show.Experiment) {
          TextHeight: 16 pts;
        }
      }
    }
  };
}

//======================================================================

class TopicSlidesClass: SlideClass {
  var Category,
      Topic,
      Groups,
      Pause;

  Constructor(ShowClass show, TitleSlideClass main, category, topic, pause) {
    super.Constructor(show, category.Title);
    Topic = topic;
    Pause = pause;

    Groups = new ListClass(8, SlideGroupClass);

    var group = new SlideGroupClass;
    Groups.Add(group);

    var height = 0.0;
    foreach (var iv in topic.Items) {
      var item = new SlideItemClass(show, iv, EachOne);
      ++show.SlideIndex;
      main.Items.Add(item);
      if (group.Items.Length > 0 && height + item.Height >= Design.SlideContent.Height) {
        group = new SlideGroupClass;
        Groups.Add(group);
        height = 0.0;
      }
      group.Items.Add(item);
      height += item.Height + Design.ItemSpacer;
    }
  }

  ShowSet(SlideGroupClass group, page, count, number) = Block {
    Design.HeaderFooter(Show, Topic.Title, page);

    Table {
      LeftIndent: 1 inch;

      Columns: [Design.BulletIndent, Design.SlideContent.Width - Design.TotalIndent];
      for (var i = 0; i < count; ++i) {
        Row {
          var item = group.Items[i];
          Cell {
            Paragraph {
              TextHeight: Design.ItemTextHeight;
              if (item.Number == number+count)
                Design.BulletCurrent;
              else
                Design.BulletDone;
            };
            Paragraph;
          };
          Cell {
            item.Content;
            PaddingB: Design.ItemSpacer;
          };
        }
      }
    };
  };

  override AddSlidePage(page) = Block {
    if (Pause) {
      var number = 0;
      foreach (var group in Groups) {
        for (var i = 1; i <= group.Items.Length; ++i)
          ShowSet(group, page, i, number);
        number += group.Items.Length;
      }
    }
    else {
      var group = Groups.FirstElement;
      ShowSet(group, page, group.Items.Length, group.Items.Length);
    }
  };
}
//======================================================================

class ShowClass: BaseViewClass {
  SlideClass[]      SlideDeck;
  PersonClass[]     AuthorList;
  ShowDesignClass   Design;
  TitleSlideClass[] Groups;
  var               SlideIndex;

  Constructor(ExperimentClass experiment, id, title, PersonClass[] authors) {
    super.Constructor(experiment, id, title);
    DocumentType = ConfigurationClass.DocumentTypes.SlideShow;
    AuthorList   = authors;
    Design       = SlideDesigns.Plain;
    SlideIndex   = 0;
  }

  ShowFigureSlide(figure, format=null) = Block {
    figure.Caption(Experiment);
    Paragraph {
      ParAlignment: ParAlignments.Center;
      format;
      figure.Body(Experiment);
    }
  };

  override GetMainDocument = Document {
    RefStyle: Style.ReferenceStyle;
    Size: DocMetrics.DocumentSize;
    Margin: Design.SlideMargin;
    Style.MainStyle;


    var slides = [new IntroSlideClass(this, Title)] +
                 SlideDeck +
                 [new EndSlideClass(this)];

    foreach (var slide in slides)
      slide.AddSlidePage(EachIndex);
  };

  AddSlideGroup(slidesvar) {
    var list = new ListClass(128);
    var groups = new ListClass(32, TitleSlideClass);
    var index  = 0;
    foreach (var slidevar in slidesvar) {
      var topics = slidevar.?Topics;
      if (exists topics) {
        var ts = new TitleSlideClass(this, slidevar);
        groups.Add(ts);
        list.Add(ts);

        foreach (var topic in topics) {
          var s;
          var figure = topic.?Figure;
          if (exists figure)
            s = new FigureSlideClass(this, ts, topic);
          else if (exists topic.?Items) {
            var pause = topic.?Pause;
            s = new TopicSlidesClass(this, ts, slidevar, topic, exists pause and pause);
          }
          else
            s = new SingleSlideClass(this, topic);
          list.Add(s);
        }
      }
      else
        list.Add(new SingleSlideClass(this, slidevar));
    }
    Groups = groups.Values;
    return list.Values;
  }
}
//======================================================================

