using Format, Units, Math, LinkTypes;

//======================================================================

class ShowDesignClass {
  SizeClass   SlidePage,
              SlideContent;
  MarginClass SlideMargin;
  var         SlideFooter,
              SlideHeader,
              HeaderGap,
              ItemTextFormat,
              BulletIndent,
              BulletSize,
              BulletDone,
              BulletCurrent,
              ItemIndent,
              ItemWidth,
              TotalIndent,
              ItemSpacer,
              TitleHeight,
              BodyHeight;


  Constructor(header, footer) {
    HeaderGap      = 12 pts;
    ItemSpacer     = 10 pts;
    TitleHeight    = 30 pts;
    BodyHeight     = 18 pts;
    BulletIndent   = 0.25 inches;
    ItemIndent     = 1 inch;
    SlideHeader    = header;
    SlideFooter    = footer;
    SlidePage      = DocMetrics.DocumentSize;
    SlideMargin    = Margin(DocMetrics.PageMargin.Left, SlideHeader + HeaderGap, DocMetrics.PageMargin.Right, SlideFooter);
    SlideContent   = Size(DocMetrics.PageContent.Width, DocMetrics.DocumentSize.Height - header - footer - HeaderGap*2);
    ItemTextFormat = {TextHeight: 20 pts};
    BulletDone     = Tex.blacksquare;
    BulletCurrent  = Tex.blacktriangleright;
    TotalIndent    = ItemIndent + BulletIndent;
    ItemWidth      = SlideContent.Width - TotalIndent - SlideMargin.Left - SlideMargin.Right;
  }

  virtual HeaderFooter(ShowClass show, SlideClass slide, title) = Block {
    Paragraph {
      PageBreakBefore: true;
    };
  };

  FixedFrame(x, y, width, height) = Frame {
    HRelative: HRelatives.Page;
    VRelative: VRelatives.Page;
    Wrapping: Wrappings.BehindText;
    X: x;
    Y: y;
    Width: width;
    Height: height;
  };
}

ShowDesignClass SlideDesigns.Plain = new(0, 0);

//======================================================================

class ProgressDesignClass: ShowDesignClass {
  Constructor {
    super.Constructor(1.5 inches, 0.36 inches);
  }

  override HeaderFooter(ShowClass show, SlideClass slide, title) = Block {
    Paragraph {
      PageBreakBefore: true;

      FixedFrame(0, 0, SlidePage.Width, SlideHeader) {
        HeaderBackground(show, slide, title, SlidePage.Width, SlideHeader);
      };
      FixedFrame(0, SlidePage.Height - SlideFooter, SlidePage.Width, SlideFooter) {
        FooterBackground(show, slide, title, SlidePage.Width, SlideFooter);
      };
    };
  };

  HeaderBackground(ShowClass show, SlideClass slide, title, width, height) = Block {
    var box = HBox {
      MarginLR: 24 pts;
      MarginT: 12 pts;
      MarginB: 12 pts;

      var i = 0;
      foreach (var group in show.Groups) {
        VBox {
          TextHeight: 10 pts;
          group.Title;
          HBox {
            for (var j = 0; j < group.BubbleCount; ++j) {
              if (i <= show.PageIndex)
                Tex.blackcirc;
              else
                Tex.bigcirc;
              ++i;
            }
          };
        };
        ++i;
      }
    };

    var bw = LayoutSize(box).Width;
    box {
      var l = show.Groups.Length - 1;
      if (l > 1)
        Separation: Math.Max([0, (DocMetrics.DocumentSize.Width - bw) / l]);
    };

    Paragraph {
      SpaceBefore: 20 pts;
      TextHeight: 30 pts;
      LeftIndent: 24 pts;
      title;
    };
  };

  FooterBackground(ShowClass show, SlideClass slide, title, width, height) = Paragraph {
    RightIndent: 10 pts;
    ParAlignment: ParAlignments.Right;
    TextHeight: 16 pts;
    show.PageIndex+1;
    " / ";
    PageCount;
  };
}
//======================================================================

class FancyDesignClass: ShowDesignClass {
  Constructor {
    super.Constructor(0.75 inches, 0.5 inches);
  }

  override HeaderFooter(ShowClass show, SlideClass slide, title) = Block {
    Paragraph {
      PageBreakBefore: true;

      if (show.PageIndex > 0) {
        FixedFrame(0, 0, SlidePage.Width, SlideHeader) {
          HeaderBackground(show, slide, title, SlidePage.Width, SlideHeader)
        };
        FixedFrame(0, SlidePage.Height -SlideFooter, SlidePage.Width, SlideFooter) {
          FooterBackground(show, slide, title, SlidePage.Width, SlideFooter);
        };
      }
    };
  };

  Design(width, height, right, bar) = Canvas {
    Height: height;

    var x0 = 0;
    var y0 = 0;
    var t  = 0.10 height;
    var x1 = 0.80 width;
    var x2 = 0.90 width;
    var p1 = Point((x2 + x1)*0.5, height * (right ? 0.25 : 0.75));

    Figure {
      Fill: bar;
      FigurePath {
        Closed: true;
        LineTo(x0, t);
        LineTo(x1, t);
        QuadTo(p1, Point(x2, height));
        LineTo(x2, height);
        LineTo(x0, height);
      }
    };

    Figure {
      Fill: Style.BarLine;
      FigurePath {
        Closed: true;
        MoveTo(x0, y0);
        LineTo(x1, y0);
        QuadTo(p1, Point(x2, height-t));
        LineTo(width, height-t);
        LineTo(width, height);
        LineTo(x2, height);
        QuadTo(p1, Point(x1, t));
        LineTo(x0, t);
      }
    };
  };

  HeaderBackground(ShowClass show, SlideClass slide, title, width, height) = Design(width, height, true, Style.HeaderLeftBar) {
    Frame {
      Width: 0.75 width;
      VAlignment: VAligns.Center;
      X: DocMetrics.PageMargin.Left;
      Paragraph {
        TextHeight: 30 pts;
        TextColor: Colors.White;
        title;
      };
    };
  };

  FooterBackground(ShowClass show, SlideClass slide, title, width, height) = Design(width, height, false, Style.LeftBar) {
    Frame {
      VAlignment: VAligns.Center;
      X: DocMetrics.PageMargin.Left;
      Width: DocMetrics.PageContent.Width;
      Paragraph {
        TabStops: [(DocMetrics.PageContent.Width) {Type: TabTypes.Right}];
        TextHeight: 16 pts;
        Span {
          TextColor: 75%;
          title
        };
        Tab;
        Span {
          TextColor: Style.HeaderBar1;
          show.PageIndex;
        }
      }
    }
  };
}
//======================================================================

class SlideClass {
  var Title;

  Constructor(title) {
    Title = title;
  }

  virtual ShowBody(ShowClass show) = null;

  virtual AddSlidePage(ShowClass show) = Block {
    show.Design.HeaderFooter(show, this, Title);
    ShowBody(show);
    show.PageIndex++;
  };
}

//======================================================================

class TitleSlideClass: SlideClass {
  ListClass<SlideItemClass> Items;
  var                       BubbleCount;

  Constructor(variable) {
    super.Constructor(variable.Title);
    Items       = new(32);
    BubbleCount = 1;
  }

  override AddSlidePage(ShowClass show) = Block {
    show.Design.HeaderFooter(show, this, null);
    Paragraph {
      ParAlignment:ParAlignments.Center;
      SpaceBefore: 1 inch;
      Style.MainFamily;
      TextHeight: show.Design.TitleHeight;
      Title;
    };
    show.PageIndex++;
  };
}

//======================================================================

class IntroSlideClass: SlideClass {
  static NameHeight  = 18 pts;
  static TitleHeight = 32 pts;
  static NameGap     = 10 pts;

  Constructor(title) {
    super.Constructor(title);
  }

  override AddSlidePage(ShowClass show) = Block {
    show.Design.HeaderFooter(show, this, null);
    ShowBody(show);
    show.PageIndex++;
  };

  override ShowBody(ShowClass show) = Block {
    var gap = (show.Design.SlideContent.Height - TitleHeight*2 - (NameGap+NameHeight)*(show.AuthorList.Length+1)) * 0.25;

    ParAlignment: ParAlignments.Center;
    TextHeight: NameHeight;

    Paragraph {
      TextHeight: TitleHeight;
      Title;
    };

    foreach (var author in show.AuthorList) {
      Paragraph {
        SpaceBefore: each0 > 0 ? NameGap : gap;
        author.First;
        Space;
        author.Last;
      };

      if (show.AuthorList.Length == 1) {
        var department = author.FindFirstFact(DepartmentLink);
        if (valid department) {
          Paragraph {
            department.Name;
          };
          var org = department.FindFirstFact(DepartmentLink);
          if (valid org) {
            Paragraph {
              org.Name;
            };
          }
        }
        Paragraph {
          var email = author.FindFirstFact(WorkEmailLink);
          if (valid email)
            email.Value;
        };
      }
    }

    Paragraph {
      SpaceBefore: gap;
      TextFormat: "dd MMM yyyy";
      System.Now;
    };
  };
}
//======================================================================

class EndSlideClass: SlideClass {
  Constructor {
    super.Constructor(Lang.Questions);
  }

  override ShowBody(ShowClass show) = Block {
    Paragraph {
      var h = 150 pts;
      ParAlignment: ParAlignments.Center;
      SpaceBefore: (show.Design.SlideContent.Height - h) * 0.5;
      Figure {
        TextHeight: h;
        Fill: (Colors.LightGreen..Colors.DarkGreen) {Vertical: true};
        Icons.help;
      }
    };
  };
}

//======================================================================

class BibSlideClass: SlideClass {
  Constructor {
    super.Constructor(Lang.References);
  }

  override AddSlidePage(ShowClass show) = Block {
    PageBreak;
    Bibliography {
      Title: Lang.References;
    };
    show.PageIndex++;
  };
}

//======================================================================

ShowItem(ShowClass show, variable) {
   var text = variable.?Text;
   if (exists text) {
     return Block {
       show.Design.ItemTextFormat;
       text;
     };
   }
   else {
     return Block {
       Style.MainStyle;
       variable.?Body(show);
     };
   }
}

class SingleSlideClass: SlideClass {
  var Variable;

  Constructor(variable) {
    super.Constructor(variable.Title);
    Variable = variable;
  }

  override ShowBody(ShowClass show) = ShowItem(show, Variable);
}

//======================================================================

class SlideItemClass {
  var Height,
      Content,
      Numbered,
      Number;

   Constructor(ShowClass show, variable, number) {
     Number   = number;
     Numbered = variable.?Numbered ?? 0;
     Content = Frame {
       Width: show.Design.ItemWidth;
       ShowItem(show, variable);
     };
     Height = LayoutSize(Content).Height;
   }
}

//======================================================================

class SlideGroupClass {
  ListClass<SlideItemClass> Items;
  SlideItemClass            Item;

  Constructor(ShowClass show, variable) {
    Items = new(8);
    Item  = new(show, variable, 0);
  }
}

//======================================================================

class FigureSlideClass: SlideClass {
  var Figure;

  Constructor(ShowClass show, topic) {
    super.Constructor(topic.?Title ?? topic.Figure.Caption(show.Experiment));
    Figure = topic.Figure;
  }

  override AddSlidePage(ShowClass show) = Block {
    show.Design.HeaderFooter(show, this, Title);
    Paragraph {
      ParAlignment: ParAlignments.Center;
      Frame {
        Figure.Body(show.Experiment) {
          TextHeight: 16 pts;
        }
      }
    }
    show.PageIndex++;
  };
}

//======================================================================

class TopicSlidesClass: SlideClass {
  SlideGroupClass[] Groups;
  var               Category,
                    Topic,
                    Pause;

  Constructor(ShowClass show, TitleSlideClass main, category, topic, pause) {
    super.Constructor(category.Title);
    Topic = topic;
    Pause = pause;

    var groups = new ListClass<SlideGroupClass>(32);
    var group  = new SlideGroupClass(show, topic);
    groups.Add(group);

    var height = 0.0;
    foreach (var iv in show.GetTopicItems(topic)) {
      var item = new SlideItemClass(show, iv, each1);
      main.Items.Add(item);

      if (group.Items.Length > 0 and height + item.Height >= show.Design.SlideContent.Height) {
        group = new SlideGroupClass(show, topic);
        groups.Add(group);
        ++show.BubbleCount;
        height = 0.0;
      }
      group.Items.Add(item);
      height += item.Height + show.Design.ItemSpacer;
    }

    main.BubbleCount = show.BubbleCount - show.BuffleFirst;
    Groups = groups.Values;
  }

  ShowSet(ShowClass show, SlideGroupClass group, count, number) = Block {
    var design = show.Design;
    design.HeaderFooter(show, this, Topic.Title);

    Table {
      LeftIndent: design.ItemIndent;

      Columns: [design.BulletIndent, design.ItemWidth];
      for (var i = 0; i < count; ++i) {
        Row {
          var gi = group.Items[i];
          Cell {
            Paragraph {
              design.ItemTextFormat;
              if (gi.Numbered > 0) {
                HBox {
                  PaddingLR: 2 pts;
                  Background: 80%;
                  gi.Numbered;
                };
              }
              else if (gi.Number == number+count)
                design.BulletCurrent;
              else
                design.BulletDone;
            };
            Paragraph;
          };
          Cell {
            gi.Content;
            PaddingB: design.ItemSpacer;
          };
        }
      }
    };
  };

  override AddSlidePage(ShowClass show) = Block {
    if (Pause) {
      var number = 0;
      foreach (var group in Groups) {
        foreach (var item in group.Items)
          ShowSet(show, group, each1, number);
        number += group.Items.Length;
        show.PageIndex++;
      }
    }
    else {
      var group = Groups.FirstElement;
      ShowSet(show, group, group.Items.Length, group.Items.Length);
      show.PageIndex++;
    }
  };
}
//======================================================================

class ShowClass: BaseViewClass {
  SlideClass[]      SlideDeck;
  PersonClass[]     AuthorList;
  ShowDesignClass   Design;
  TitleSlideClass[] Groups;
  var               TypeFlags,
                    PageIndex,
                    BuffleFirst,
                    BubbleCount;

  Constructor(ExperimentClass experiment, id, title, PersonClass[] authors) {
    super.Constructor(experiment, id, title);
    DocumentType = ConfigurationClass.DocumentTypes.SlideShow;
    AuthorList   = authors;
    Design       = SlideDesigns.Plain;
    TypeFlags    = 0;
  }

  GetTopicItems(topic) {
    var items = topic.?Items;
    if (not exists items)
      items = topic.?GetItems(this);
    return items ?? [];
  }

  ShowFigureSlide(figure, format=null) = Block {
    figure.Caption(Experiment);
    Paragraph {
      ParAlignment: ParAlignments.Center;
      format;
      figure.Body(Experiment);
    }
  };

  override GetMainDocument = Document {
    RefStyle: Style.ReferenceStyle;
    Size: DocMetrics.DocumentSize;
    Margin: Design.SlideMargin;
    Style.MainStyle;


    var slides = [new IntroSlideClass(Title)] +
                 SlideDeck +
                 [new EndSlideClass, new BibSlideClass];

    PageIndex = 0;
    foreach (var slide in slides)
      slide.AddSlidePage(this);
  };

  CanInclude(slide) {
    return TypeFlags == 0 or not valid slide.?Type or (Integer(slide.Type) & TypeFlags) != 0;
  }

  AddSlideGroup(slidesvar) {
    var types  = Integer(TypeFlags);
    var list   = new ListClass<SlideClass>(128);
    var groups = new ListClass<TitleSlideClass>(32);

    BuffleFirst = 0;
    BubbleCount = 0;
    foreach (var slidevar in slidesvar) {
      if (CanInclude(slidevar)) {
        var topics = slidevar.?Topics;
        if (exists topics) {
          ++BubbleCount;
          var ts = new TitleSlideClass(slidevar);
          groups.Add(ts);
          list.Add(ts);

          foreach (var topic in topics) {
            if (CanInclude(topic)) {
              ++BubbleCount;

              SlideClass s;
              var figure = topic.?Figure;
              if (exists figure)
                s = new FigureSlideClass(this, topic);
              else {
                var items = GetTopicItems(topic);
                if (items.Length > 0) {
                  var pause = topic.?Pause;
                  s = new TopicSlidesClass(this, ts, slidevar, topic, exists pause and pause);
                }
                else
                  s = new SingleSlideClass(topic);
              }
              list.Add(s);
            }
          }
        }
        else {
          ++BubbleCount;
          list.Add(new SingleSlideClass(slidevar));
        }
      }
      BuffleFirst = BubbleCount;
    }
    Groups = groups.Values;
    return list.Values;
  }
}
//======================================================================

