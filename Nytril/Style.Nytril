using Format, Units, Math, IO;
//======================================================================

with PaperSizes {
  Letter = Size(8.5 inches, 11 inches);
  A4     = Size(210 millimeters, 297 millimeters);
}
//======================================================================

with DocMetrics if (PaperSystem == PaperSystems.US) {
  DocumentSize = PaperSizes.Letter;
  Gutter       = 0;
  PageMargin   = Margin(0.75 inches);
}
//======================================================================

with DocMetrics if (PaperSystem == PaperSystems.A) {
  DocumentSize = PaperSizes.A4;
  Gutter       = 0;
  PageMargin   = Margin(20 millimeters);
}
//======================================================================

with DocMetrics {
  PageContent    = DocumentSize - PageMargin - Size(Gutter, 0);
  TableSpace     = 24 pts;
  TreeWidth      = PageContent.Width;
  BoxSize        = Size(18 pts, 20 pts);
  CellSize       = Size(BoxSize.Width * 2, BoxSize.Height);
  SegmentColumns = 4;
  SegmentSize    = 40 pts;
}
//======================================================================

class AppendixClass {
  var Title,
      Content;

  Constructor(title, content) {
    Title   =  title;
    Content = content;
  }
}
//======================================================================

with Style {
  MainFamily           = {TextFamily: TextFamilies.TimesNewRoman};
  SansSerif            = {TextFamily: TextFamilies.Calibri};
  MonoFamily           = {TextFamily: TextFamilies.Consolas};
  IPAFamily            = {TextFamily: TextFamilies.CambriaMath}; // Also can be "Linux Libertine O"

  ImpossibleBackground = {Background: 80%};
  CommentColor         = Color(0, 0x64, 0);
  StringColor          = Color(168, 21, 53);
  Used                 = {TextColor: Colors.Red};
  TitleBackground      = {Background: 90%};
  TableEdge            = {Edge: 0.3 pts};
  ColumnEdge           = {EdgeR: 0.3 pts};
  SegmentBottom        = {EdgeB: 0.25 pts {Color: 80%}};

  Word(w) = w Italic;

  RowBar(i) = {
    if (i mod 2 != 0)
      Background: 95%;
  };

  WhitePaper = Document {
    Size: DocMetrics.DocumentSize;
    Margin: DocMetrics.PageMargin;
    MainFamily;
    TextHeight: 11 pts;
  };

  NormalHeader(text) = Block {
    Span {TextCase: TextCases.AllUpper; text}
  };

  PageSection = Section {
    SectionBreak: SectionBreaks.NextPage;
    Footer: Block {
      Distance: 0.5 inches;
      ParAlignment: ParAlignments.Center;
      PageNumber
    };
  };

  TableNotes = Paragraph {
    SpaceAfter: DocMetrics.TableSpace;
  };

  TitleCase(text) = Span {
    if (exists text) {
      TextCase: TextCases.SmallCaps;
      Separator: Space;
      text;
    }
  };

  Title(text) = Paragraph {
    KeepWithNext: true;
    SpaceBefore: 12 pts;
    SpaceAfter: 6 pts;
    ParAlignment: ParAlignments.Center;
    TextHeight: 18 pts;
    text
  };

  HeaderCentered(text) = Paragraph {
    KeepWithNext: true;
    SpaceBefore: 12 pts;
    SpaceAfter: 6 pts;
    ParAlignment: ParAlignments.Center;
    TitleCase(text)
  };

  Header1(text) = Paragraph {
    KeepWithNext: true;
    SpaceBefore: 18 pts;
    SpaceAfter: 8 pts;
    TitleCase(text)
  };

  Header2(text) = Paragraph {
    KeepWithNext: true;
    SpaceBefore: 12 pts;
    SpaceAfter: 6 pts;
    TextHeight: 14 pts;
    text
  };

  Header3(text) = Paragraph {
    KeepWithNext: true;
    Border: 0.25 pts;
    ParBackground: 97%;
    SpaceAfter: 12 pts;
    TextHeight: 14 pts;
    text
  };

  SourceFile(source) = Block {
    Style.Header3(GetFileName(source.Path));
    Paragraph {
      LeftIndent: 0.25 inches;
      ParAlignment: ParAlignments.Left;
      Style.MonoFamily;
      TextHeight: 8 pts;
      SourceSelection(source)
    }
  };

  SourceCodeBlock = TextBlock {
    Style.MonoFamily;
    TextHeight: 10 pts;
    ParBackground: 97%;
  };

  Author(author) = Span {
    Link: author.GlobalVariablePath;
    author.Title;
  };

  void Collect(list, node) {
    if (node.Label)
      list.Add(node, node.Label);
    Collect(list, each node);
  }

  GetTimes(node) {
    var list = new ListClass(100);
    Collect(list, node);
    return list[1..];
  }

  ByYear(x, y) = x.BranchLength.Compare(y.BranchLength);

  TimelineNodes = GetTimes(Info.LanguageTree).Sort(true, ByYear);

  TimelineRow(options, node) = Group {
    X: options.Width - options.Width * node.BranchLength / options.MaxYear;
    Figure {
      Fill: Colors.Green;
      Rectangle(Rect(Point(0), Size(5, options.TextHeight)))
    };
    Paragraph {
      Space;
      node.Label;
    }
  };

  ReadTree(title, path) {
    var data = IO.Read(path, FileFormats.Nexus);
    return Style.ShowTree(title, Lang.ChangesPerBranch, data.trees.TREE1);
  }

  ShowTree(title, axis, root) = Group {
    Vertical: true;
    Corner: 8 pts;
    MarginB: 20 pts;
    PaddingLR: 15 pts;
    Border: 0.5 pts {Color: 90%};
    Background: 98%;
    Paragraph {
      HAlignment: HAligns.Center;
      TextHeight: 14 pts;
      Bold;
      title;
    };
    Tree {
      Curvature: 30%;
      Bevel: 20%;
      Marker: {Style.IPAFamily; TextHeight: 4 pts; TextColor: Colors.Gray} Chars.blackcirc;
      Width: DocMetrics.TreeWidth - 30 pts;
      ValueLabel: axis;
      ValueAxis: ChartAxis;
      Root: root;
    };
  };

  ShowContent(content) = Block {
    Header1(content.Title);
    Block {
      content.Body;
    }
  };

  ShowAuthorFull(author) = Block {
    LeftIndent: 0.25 inches;
    Paragraph {
      LocationMark: author.GlobalVariablePath;
      FirstIndent: -0.25 inches;
      Span {
        Separator: Space;
        if (exists author.Website)
          Link: author.Website;
        author.First;
        author.Middle;
        author.Last;
      };
    };
    Span {
      Separator: ", ";
      author.Address
    };
    Span {
      "{0}: "(Lang.EMail);
      author.EMail;
    };
    Empty;
  };

  AppendixRow(appendix) = Paragraph {
    LeftIndent: 20 pts;
    FirstIndent: -20 pts;
    EachIndex + 1;
    ".";
    Tab;
    Span {
      Link: Lang.Appendix + EachIndex;
      appendix.Title
    };
  };

  ShowAuthors(authors) = Block {
    HeaderCentered(Lang.Authors);
    ShowAuthorFull(each authors);
  };

  ShowReference(r) = Paragraph {
    LeftIndent: 0.25 inches;
    FirstIndent: -0.25 inches;
    if (exists r.Author) {
      TitleCase(r.Author);
      if (exists r.Title) {
        ", ";
        r.Title;
      }
    }
    else
      r.Title;

    if (exists r.Year) {
      ", ";
      r.Year;
    }
    if (exists r.Publisher) {
      ", ";
      Italic r.Publisher;
      r.?Page;
    }
    if (exists r.?Link) {
      ". : ";
      Span {
        Link: r.Link;
        TextColor: Colors.DarkBlue;
        r.Link;
      };
    }
  };

  ShowReferences(references) = Block {
    HeaderCentered(Lang.References);
    ShowReference(each references)
  };

  ShowAppendix(appendix) = PageSection {
    Header: Block {
      Paragraph {
        BorderB: 1 pts;
        ParAlignment: ParAlignments.Center;
        TextHeight: 14 pts;
        SpaceAfter: 8 pts;
        LocationMark: Lang.Appendix + EachIndex;
        "{0} {1} - "(Lang.Appendix, EachIndex+1);
        appendix.Title;
      };
    };
    Block {
      appendix.Content;
    };
  };

  LanguageRow(lang) = Row {
    Background: ((EachIndex mod 2) == 0 ? Colors.White : 97%);
    lang.GlobalVariableName;
  };

  ShowLanguageList(list) = Table {
    PaddingLR: 2;
    Columns: [1.5 inch];
    Row {
      Background: Colors.DarkGray;
      TextColor: Colors.White;
      Lang.Name;
    };
    Edge: 0.25 pts {Color: Colors.DarkGray};
    LanguageRow(each list)
  };

  HeaderCell(d, halign=HAligns.Left) = Cell {
    HAlign: halign;
    VAlign: VAligns.Center;
    Style.SansSerif;
    Style.TitleBackground;
    EdgeB: 1 pts;
    Padding: 2 pts;
    d
  };

  TitleBar(name, columns) = Row {
    Cell {
      Padding: 2 pts;
      ParAlignment: ParAlignments.Center;
      Background: 40%;
      TextHeight: 16 pts;
      TextColor: Colors.White;
      ColSpan: columns;
      name
    }
  };

  BulletList = ListBlock {
    Marker: Chars.bullet;
    LeftIndent: 24 pts;
    FirstIndent: -16 pts;
  };

  NumberList = ListBlock {
    Marker: i => (i+1) ".";
    LeftIndent: 24 pts;
    FirstIndent: -16 pts;
  };

  BodyPar = Paragraph {
    ParAlignment: ParAlignments.Justify;
    SpaceBefore: 6 pts;
  };
}
//======================================================================

Logo = Frame {
  Size: Size(5 inches, 0.5 inches);
  Padding: 4 pts;
  Background: Color(51, 66, 81);
  Table {
    Columns: [4.3 inches, 0.6 inches];
    Row {
      Cell {
        Paragraph {
          TextColor: Color(129, 166, 207);
          Bold;
          TextHeight: 12 pts;
          "Transactions of the";
        };
        Paragraph {
          Bold;
          TextHeight: 20 pts;
          TextColor: Colors.White;
          "Bayesian Society"
        };
      };
      Cell {
        Read(Folders.Source FileName("bayes") Extensions.PNG) {Width: 0.5 inches}
      };
    }
  }
};
//======================================================================

