using Format, Units, Math, IO
//======================================================================

with Metrics
  let MarginL = 0.75 inches
  let MarginR = MarginL
  let MarginT = 0.5 inches
  let MarginB = 0.4 inches
  let Paper   = Type.Size(8.5 inches, 11 inches)
  let Content = Type.Size(Paper.Width - MarginL - MarginR, Paper.Height - MarginT - MarginB)
  let TableSpace = 24 pts
end
//======================================================================

with Style
  let Arial = "Arial Unicode MS"

  let MainFamily           = {TextFamily: TextFamilies.TimesNewRoman}
  let SansSerif            = {TextFamily: Arial}
  let MonoFamily           = {TextFamily: TextFamilies.Consolas}
  let IPAFamily            = {TextFamily: Arial} // Also can be TextFamilies.CambriaMath

  let ImpossibleBackground = {Background: 80%}
  let Used                 = {TextColor: Colors.Red}
  let TitleBackground      = {Background: 90%}
  let TableEdge            = {Edge: 0.3 pts}
  let ColumnEdge           = {EdgeR: 0.3 pts}
  let SoundBottom          = {EdgeB: 0.25 pts {Color: 80%}}

  let RowBar(i) = {
    if (i mod 2 != 0)
      Background: 95%
    end
  }

  let WhitePaper = Document {
    Size: Metrics.Paper,
    MainFamily,
    TextHeight: 11.5 pts,
    MarginL: Metrics.MarginL,
    MarginR: Metrics.MarginR,
    MarginT: Metrics.MarginT,
    MarginB: Metrics.MarginB,
  }

  let NormalHeader(text) = Block {
    Span {{TextUppercase: true} text}
  }

  let PageSection = Section {
    SectionBreak: SectionBreaks.NextPage,
    Footer: Block {
      Distance: 0.5 inches,
      ParAlignment: ParAlignments.Center,
      PageNumber
    },
  }

  let TableNotes = Paragraph {
    SpaceAfter: Metrics.TableSpace,
  }

  let TitleWord(text) = Span {
    text[0],
    {TextHeight: 60%} text[1..]
  }

  let TitleCase(text) = Span {
    if (text)
      TextUppercase: true,
      Separator: Space,
      TitleWord(each text.Split(Space))
    end
  }

  let Title(text) = Paragraph {
    KeepWithNext: true,
    SpaceBefore: 12 pts,
    SpaceAfter: 6 pts,
    ParAlignment: ParAlignments.Center,
    TextHeight: 18 pts,
    text
  }

  let HeaderCentered(text) = Paragraph {
    KeepWithNext: true,
    SpaceBefore: 12 pts,
    SpaceAfter: 6 pts,
    ParAlignment: ParAlignments.Center,
    TitleCase(text)
  }

  let Header1(text) = Paragraph {
    KeepWithNext: true,
    SpaceBefore: 18 pts,
    SpaceAfter: 8 pts,
    TitleCase(text)
  }

  let Header2(text) = Paragraph {
    KeepWithNext: true,
    SpaceBefore: 12 pts,
    SpaceAfter: 6 pts,
    TextHeight: 14 pts,
    text
  }

  let Header3(text) = Paragraph {
    KeepWithNext: true,
    Border: 0.25 pts,
    ParBackground: 97%,
    SpaceAfter: 12 pts,
    TextHeight: 14 pts,
    text
  }

  let SourceFile(ref source) = Block {
    Style.Header3(source.Path.GetFileName),
    Paragraph {
      LeftIndent: 0.25 inches,
      ParAlignment: ParAlignments.Left,
      Style.MonoFamily,
      TextHeight: 8 pts,
      SourceSelection(source)
    }
  }

  let SourceCodeBlock = TextBlock {
    Style.MonoFamily,
    TextHeight: 10 pts,
    ParBackground: 97%,
  }

  let Author(author) = Span {
    Link: author.FullSymbolName,
    author.Title,
  }

  let DataLabel(node) = node.Data.Name

  let ShowTree(tree) = Tree {
//    TextHeight: 5.5 pts,
    Label: DataLabel,
    Curvature: 30%,
    Bevel: 20%,
    Marker: {Style.IPAFamily, TextHeight: 4 pts}Chars.Circle {TextColor: Colors.Gray},
    Width: 6 inches,
    tree
  }

  let ShowAbstract(content) = Block {
    HeaderCentered(content.Title),
    Block {
      content.Body
    }
  }

  let ShowContent(content) = Block {
    Header1(content.Title),
    Block {
      content.Body
    }
  }

  let ShowAuthorFull(author) = Block {
    LeftIndent: 0.25 inches,
    Paragraph {
      LocationMark: author.FullSymbolName,
      FirstIndent: -0.25 inches,
      Span {
        Separator: Space,
        if (author.Website)
          TextColor: Colors.Blue,
          Link: author.Website,
        end,
        author.First, author.Middle, author.Last,
      }
    },
    Span {
      Separator: Lang.Separator,
      author.Address
    },
    Span {"{0}: "(Lang.EMail), author.EMail},
    Empty
  }

  let AppendixRow(ref appendix) = Row {
    Cell {
      Span {EachIndex + 1, ")"}
    },
    Cell {
      Span {
        Link: appendix.FullSymbolName,
        appendix.Title
      }
    }
  }

  let ShowAppendixTable = Block {
    HeaderCentered(Lang.Appendices),
    Table {
      Columns: [15 pts, 5 inches],
      AppendixRow(each Appendix)
    }
  }

  let ShowAuthors(authors) = Block {
    HeaderCentered(Lang.Authors),
    ShowAuthorFull(each authors),
  }

  let ShowReference(ref r) = Paragraph {
    LeftIndent: 0.25 inches,
    FirstIndent: -0.25 inches,
    if (r.Author)
      TitleCase(r.Author),
      if (r.Title)
        Lang.Separator,
        r.Title,
      end,
    else
    r.Title
    end,
    if (r.Year)
      Lang.Separator,
      r.Year,
    end,
    if (r.Publisher)
      Lang.Separator,
      Italic r.Publisher,
      r.Page,
    end,
    if (r.Link)
      ". {0}: "(Lang.AvailableAt),
      Span {Link: r.Link, TextColor: Colors.DarkBlue, r.Link}
    end,
  }

  let ShowReferences(references) = Block {
    HeaderCentered(Lang.References),
    ShowReference(each references)
  }

  let ShowAppendix(ref appendix) = PageSection {
    Paragraph {
      BorderB: 1 pts,
      ParAlignment: ParAlignments.Center,
      TextHeight: 14 pts,
      SpaceAfter: 8 pts,
      LocationMark: appendix.FullSymbolName,
      "{0} {1} - "(Lang.Appendix, EachIndex+1),
      appendix.Title,
    },
    Block {
      appendix.Content
    }
  }

  let LanguageRow(lang) = Row {
    Background: ((EachIndex mod 2) == 0 ? Colors.White : 97%),
    lang.Name,
  }

  let ShowLanguageList(list) = Table {
    PaddingLR: 2,
    Columns: [1.5 inch],
    Row {
      Background: Colors.DarkGray,
      TextColor: Colors.White,
      Lang.Name,
    },
    Edge: 0.25 pts {Color: Colors.DarkGray},
    LanguageRow(each list)
  }

  let HeaderCell(d) = Cell {
    HAlign: HAligns.Center,
    VAlign: VAligns.Center,
    Style.SansSerif,
    Style.TitleBackground,
    EdgeB: 1 pts,
    Padding: 2 pts,
    d
  }

  let TitleBar(name, columns) = Row {
    Cell {
      Padding: 2 pts,
      ParAlignment: ParAlignments.Center,
      Background: 40%,
      TextHeight: 16 pts,
      TextColor: Colors.White,
      ColumnSpan: columns,
      name
    }
  }
end
//======================================================================

with DisplayWords
  let WordRow(word) begin
    var sounds = IPA.SAMPAToSounds(word.Text);

    return Row {
      Style.RowBar(EachIndex),
      Cell {
        Style.SansSerif,
        Style.TitleBackground,
        word.Meaning
      },
      Cell {
        word.Text
      },
      Cell {
        Span {
          if (sounds)
            IPA.ShowSound(each sounds)
          end
        },
      },
      Cell {
        Span {
          Separator: Lang.Separator,
          (each sounds).SymbolName
        }
      }
    }
  end

  let HeaderCell(d) = Cell {
    Style.SansSerif,
    Style.TitleBackground,
    EdgeB: 1 pts,
    Padding: 2 pts,
    d
  }

  let LanguageTable(language) = Block {
    Table {
      Columns: [0.8 inches, 0.8 inches, 0.9 inches, Metrics.Content.Width - 2.7 inches],
      Style.TitleBar(language.Name, 4),
      Row {
        HeaderCell(Lang.Meaning),
        HeaderCell(Lang.SAMPA),
        HeaderCell(Lang.IPA),
        HeaderCell(Lang.Sounds),
      },
      WordRow(each language.Words)
    },
    Style.TableNotes
  }
end
//======================================================================

with AllWords
  let LangHasMeaning(word, data) = word.Meaning == data.Meaning

  let AddCell(meaning, language) begin
    var list = language.Words.FindSlice(LangHasMeaning, {Meaning: meaning});
    return Cell {
      (each list).Text
    }
  end

  let AddRow(wordlist, meaning) = Row {
    Style.RowBar(EachIndex),
    Cell {
      Style.TitleBackground,
      Style.SansSerif,
      meaning
    },
    AddCell(meaning, each wordlist)
  }

  let HeaderCell(d) = Cell {
    Style.SansSerif,
    Style.TitleBackground,
    EdgeB: 1 pts,
    Padding: 2 pts,
    d
  }

  let LanguageHeader(language) = HeaderCell(language.Name)

  let Column1 = 36 pts

  let WordTable(wordlist) = Block {
    Table {
      TextHeight: 7 pts,
      Columns: [Column1, (Metrics.Content.Width - Column1) / wordlist.Length],
      Style.TitleBar("Words in each language by meaning", wordlist.Length+1),
      Row {
        HeaderCell({TextColor: Colors.DarkGray} Lang.Meaning),
        LanguageHeader(each wordlist),
      },
      AddRow(wordlist, each WordMeanings)
    },
    Style.TableNotes
  }
end
//======================================================================

let Logo = Frame {
  Width: 5 inches,
  Height: 0.5 inches,
  Padding: 4 pts,
  Background: Type.Color(51, 66, 81),
  Table {
    Columns: [4.3 inches, 0.6 inches],
    Row {
      Block {
        "Transactions of the" {TextColor: Type.Color(129, 166, 207), Bold, TextHeight: 12 pts},
        "Bayesian Society" {Bold, TextHeight: 20 pts, TextColor: Colors.White}
      },
      Read(Folders.Source FileName("bayes") Extensions.PNG) {Width: 0.5 inches}
    }
  }
}
//======================================================================

