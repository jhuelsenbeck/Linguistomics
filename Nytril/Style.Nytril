using Format, Units, Math, IO;
//======================================================================

with PaperSizes {
  Letter = Size(8.5 inches, 11 inches);
  A4     = Size(210 millimeters, 297 millimeters);
}
//======================================================================

with DocMetrics if (PaperSystem == PaperSystems.US and DocumentType == DocumentTypes.Normal) {
  DocumentSize = PaperSizes.Letter;
  PageMargin   = Margin(0.8 inches, 0.9 inches, 0.8 inches, 1 inches);
}

with DocMetrics if (PaperSystem == PaperSystems.US and DocumentType == DocumentTypes.SlideShow) {
  DocumentSize = PaperSizes.Letter.Landscape;
  PageMargin   = Margin(0.5 inches, 1.5 inches, 0.5 inches, 0.75 inches);
}

with DocMetrics if (PaperSystem == PaperSystems.A and DocumentType == DocumentTypes.Normal) {
  DocumentSize = PaperSizes.A4;
  PageMargin   = Margin(20 millimeters);
}

with DocMetrics if (PaperSystem == PaperSystems.A and DocumentType == DocumentTypes.SlideShow) {
  DocumentSize = PaperSizes.A4.Landscape;
  PageMargin   = Margin(0.5 inches, 1.5 inches, 0.5 inches, 0.75 inches);
}
//======================================================================

with DocMetrics {
  PageContent    = DocumentSize - PageMargin - Size(Gutter, 0);
  TableSpace     = 24 pts;
  TreeWidth      = PageContent.Width;
  BoxSize        = Size(18 pts, 20 pts);
  CellSize       = Size(BoxSize.Width * 2, BoxSize.Height);
  Gutter         = 0;
  SegmentColumns = 4;
  SegmentSize    = 40 pts;
  SlideFooter    = 0.5 inches;
  SlideHeader    = 1 inches;
  SlideContent   = Size(PageContent.Width, PageContent.Height - SlideFooter);
}
//======================================================================

class AppendixClass {
  var Title,
      Content;

  Constructor(title, content) {
    Title   =  title;
    Content = content;
  }
}
//======================================================================

class DefClass {
  var Symbol,
      Name,
      Description;

  Constructor(symbol, name, description) {
    Symbol      = symbol;
    Name        = name;
    Description = description;
  }

  override GetLayout = Span {
    Popup: this;
    Symbol;
  };

  override GetPopup = Block {
    Paragraph {
      TextHeight: 20 pts;
      Style.Equation {Symbol};
    };
    Paragraph {
      Bold; Name;
    };
    Paragraph {
      Description;
    };
  };

  Appendix = Block {
    Paragraph {
      TextHeight: 16 pts;
      Style.Equation {Symbol};
      Tab;
      Name;
    };
    Paragraph {
      LeftIndent: 0.5 inches;
      Description;
    };
  };
}
//======================================================================

Main.MathFamily = Style.MathFace; //"Linux Libertine Display O";

with Style {
  MathFace             = "CMU Serif";
  GreekFamily          = {TextFamily: MathFace};
  MainFamily           = {TextFamily: MathFace};
  Classical            = {TextFamily: "CMU Classical Serif"};
  Extra                = {TextFamily: "CMU Serif Extra"};
  SansSerif            = {TextFamily: "CMU Sans Serif"};
  MonoFamily           = {TextFamily: "CMU Typewriter Text"};
  IPAFamily            = {TextFamily: "Linux Libertine O"};

  ImpossibleBackground = {Background: 80%};
  CommentColor         = Color(0, 0x64, 0);
  StringColor          = Color(168, 21, 53);
  NoteIndexIndent      = 6 pts;
  VariableColor        = Colors.DarkBlue;
  AlignBackColor       = 95%;
  Used                 = {TextColor: Colors.Red};
  TitleBackground      = {Background: 90%};
  TableEdgeSize        = 0.25 pts;
  TableEdge            = {Edge: TableEdgeSize};
  ColumnEdge           = {EdgeR: TableEdgeSize};
  SegmentBottom        = {EdgeB: TableEdgeSize {Color: 80%}};
  Logo1                = Color(0, 0, 0xFF);
  Logo1A               = Color(0, 0, 0xE0);
  Logo2                = Color(0xFF, 0xA5, 00);
  LightColor           = Colors.Orange;
  DarkColor            = Colors.DarkBlue;
  ArrowColor           = Colors.LightGray;
  HighlightColor       = Colors.Orange;
  TextColor            = Colors.Black;
  SectionBack          = DarkColor;
  SectionFore          = Colors.White;
  HeaderText           = Colors.White;
  SlideHeader          = Colors.SlateGray;
  Hyperlink            = Color(100, 42, 143);
  HeaderBar1           = Color(23, 54, 114);
  HeaderBar2           = 99%;
  RightBar             = null;
  LeftBar              = (90%)..(99%);
  BarLine              = LightColor;
  FooterBar1           = Color(223, 232, 249);
  FooterBar2           = Colors.White;
  TitleColor           = Colors.Orange;
  HeaderLeftBar        = HeaderBar1..HeaderBar2;
  HeaderRightBar       = (99%)..(90%);
  FooterLeftBar        = FooterBar1..FooterBar2;
  FooterRightBar       = FooterBar2..FooterBar1;
  Word(w)              = Span {TextItalic: true; w};
  DoubleQuote          = Span {Begin: "“"; End: "”"};
  SingleQuote          = Span {Begin: "‘"; End: "’"};
  AndSpace             = " and ";
  ColonSpace           = ": ";
  Comma                = ",";
  eos                  = ". ";

  MainDocument = Document {
    RefStyle: new ChicagoStyleClass;
  //  RefStyle: new MLAStyleClass;

    Size: DocMetrics.DocumentSize;
    Margin: DocMetrics.PageMargin;
    MainFamily;
  };

  MainTitle = Paragraph {
    TextFamily: TextFamilies.Arial;
    TextHeight: 20 pts;
    Bold;
    SpaceAfter: 10 pts;
  };

  AppendixTitle = Paragraph {
    TextFamily: TextFamilies.Arial;
    TextHeight: 16 pts;
    Bold;
    SpaceAfter: 10 pts;
  };

  ShowAuthor(PersonClass person) = Span {
    person.GetFullName {Bold};
    Span {
      Separator: Comma;
      TextAlignment: TextAlignments.Superscript;
      EachOne;
      "+";
    };

    var site = person.FindFirstFact(LinkTypes.WorkWebsite);
    if (valid site)
      Link: site.Value;
  };

  ShowAuthorDetails(PersonClass person) {
    var department = person.FindFirstFact(LinkTypes.WorkDepartment);
    if (valid department) {
      return Block {
        LeftIndent: NoteIndexIndent;
        Paragraph {
          FirstIndent: -NoteIndexIndent;
          Span {
            TextAlignment: TextAlignments.Superscript;
            EachOne;
          };
          Tab;
          department.Name;

          var org = department.FindFirstFact(LinkTypes.Department) ?? department;
          if (valid org) {
            if (org != department) {
              CommaSpace;
              org.Name;
            }

            var address = org.FindFirstFact(LinkTypes.MainOffice);
            if (valid address) {
              CommaSpace;
              address.ShowSpan(true);
            }
          }
        };

        var email = person.FindFirstFact(LinkTypes.WorkEmail);
        if (valid email) {
          Paragraph {
            Span {
              email.Value;
              Link: "mailto:" + email.Value;
            }
          };
        }
      };
    }
    return null;
  }

  ShowAuthorList(authors) = Block {
    TextFamily: TextFamilies.Arial;
    Paragraph {
      TextHeight: 12 pts;
      SpaceAfter: 10 pts;
      Separator: CommaSpace;
      LastSeparator: AndSpace;
      ShowAuthor(each authors);
    };

    Block {
      TextHeight: 10 pts;
      ShowAuthorDetails(each authors);
      Paragraph {
        LeftIndent: NoteIndexIndent;
        FirstIndent: -NoteIndexIndent;
        Span {
          TextAlignment: TextAlignments.Superscript;
          "+";
        };
        Tab;
        "these authors contributed equally to this work";
      };
    };
  };

  ShowAuthorFull(PersonClass person) = Block {
    LeftIndent: 0.25 inches;

    Paragraph {
      LocationMark: person.GlobalVariablePath;
      FirstIndent: -0.25 inches;
      person.GetFullName;
    };

    person.FindFirstFact(LinkTypes.WorkAddress)?.ShowSpan(true);

    var email = person.FindFirstFact(LinkTypes.WorkEmail);
    if (valid email) {
      Paragraph {
        Lang.EMail;
        ColonSpace;
        Span {
          email.Value;
          Link: "mailto:" + email.Value;
        }
      };
    }

    var site = person.FindFirstFact(LinkTypes.WorkWebsite);
    if (valid site) {
      Paragraph {
        Seperator: CommaSpace;
        Span {
          Link: site.Value;
          site.Value;
        }
      };
    }
    Empty;
  };

  RowBar(i) = {
    if (i mod 2 != 0)
      Background: 95%;
  };

  PageSection = Section {
    SectionBreak: SectionBreaks.NextPage;
    Footer: Block {
      Distance: 0.5 inches;
      ParAlignment: ParAlignments.Center;
      PageNumber;
    };
  };

  Bibliography = Format.Bibliography;

  TableNotes = Paragraph {
    SpaceAfter: DocMetrics.TableSpace;
  };

  AnimalRow(a) = Row {
    TextHeight: 10 pts;
    a.GlobalVariableName;
    Cell {
      Span {
        Style.MonoFamily;
        a;
      }
      Tex.ldots;
    }
  };

  HeaderCentered = Paragraph {
    KeepWithNext: true;
    SpaceBefore: 12 pts;
    SpaceAfter: 6 pts;
    ParAlignment: ParAlignments.Center;
    TextCase: TextCases.SmallCaps;
  };

  Header1 = Paragraph {
    KeepWithNext: true;
    SpaceBefore: 18 pts;
    SpaceAfter: 8 pts;
    TextCase: TextCases.SmallCaps;
  };

  Header2 = Paragraph {
    KeepWithNext: true;
    SpaceBefore: 12 pts;
    SpaceAfter: 6 pts;
    TextHeight: 14 pts;
  };

  Header3 = Paragraph {
    KeepWithNext: true;
    Border: 0.25 pts;
    ParBackground: 97%;
    SpaceAfter: 12 pts;
    TextHeight: 14 pts;
  };

  SourceFile(source) = Block {
    Style.Header3(GetFileName(source.Path));
    Paragraph {
      LeftIndent: 0.25 inches;
      ParAlignment: ParAlignments.Left;
      Style.MonoFamily;
      TextHeight: 8 pts;
      SourceSelection(source)
    }
  };

  SourceCodeBlock = TextBlock {
    Style.MonoFamily;
    TextHeight: 10 pts;
    ParBackground: 97%;
  };

  Definition(s) = Span {
    Italic;
    s;
  };

  void Collect(list, node) {
    if (node.Label)
      list.Add(node, node.Label);
    Collect(list, each node);
  }

  GetTimes(node) {
    var list = new ListClass(100);
    Collect(list, node);
    return list[1..];
  }

  ByYear(x, y) = x.BranchLength.Compare(y.BranchLength);

  TimelineNodes = GetTimes(Info.LanguageTree).Sort(true, ByYear);

  TimelineRow(options, node) = HBox {
    X: options.Width - options.Width * node.BranchLength / options.MaxYear;
    Figure {
      Fill: Colors.Green;
      Rectangle(Rect(Point(0), Size(5, options.TextHeight)))
    };
    Paragraph {
      Space;
      node.Label;
    }
  };

  NumberedList = ListBlock {
    ParAlignment: ParAlignments.Justify;
    LeftIndent: 0.5 inches;
    FirstIndent: -0.25 inches;
    Marker: i => "{0}."(i+1);
  };

  ReadTree(title, path) {
    var data = IO.Read(path, FileFormats.Nexus);
    return Style.ShowTree(title, Lang.ChangesPerBranch, data.trees.TREE1);
  }

  TreeOptions = Tree {
    Style.SansSerif;
    TextHeight: 10 pts;
    NodeGap: 6 pts;
    Width: 5 inches;
    Curvature: 30%;
    Bevel: 20%;
    Marker: Span {
      Style.IPAFamily;
      TextHeight: 4 pts;
      TextColor: Colors.Gray;
      Tex.blackcirc;
    };
  };

  ShowTree(title, axis, root) = VBox {
    Corner: 8 pts;
    MarginB: 20 pts;
    PaddingLR: 15 pts;
    Border: 0.5 pts {Color: 90%};
    Paragraph {
      HAlignment: HAligns.Center;
      TextHeight: 14 pts;
      Bold;
      title;
    };
    TreeOptions {
      Width: DocMetrics.TreeWidth - 30 pts;
      ValueLabel: axis;
      ValueAxis: ChartAxis;
      Root: root;
    };
  };

  ShowContent(content) = Block {
    Header1 {
      content.Title;
    };
    content.Body;
  };

  AppendixRow(appendix) = Paragraph {
    LeftIndent: 20 pts;
    FirstIndent: -20 pts;
    EachOne;
    ".";
    Tab;
    Span {
      Link: Lang.Appendix + EachIndex;
      appendix.Title
    };
  };

  ShowAppendix(appendix) = PageSection {
    Header: Block {
      Paragraph {
        BorderB: 1 pts;
        ParAlignment: ParAlignments.Center;
        TextHeight: 14 pts;
        SpaceAfter: 8 pts;
        LocationMark: Lang.Appendix + EachIndex;
        "{0} {1} - "(Lang.Appendix, EachOne);
        appendix.Title;
      };
    };
    Block {
      appendix.Content;
    };
  };

  LanguageRow(lang) = Row {
    Background: ((EachIndex mod 2) == 0 ? Colors.White : 97%);
    lang.GlobalVariableName;
  };

  ShowLanguageList(list) = Table {
    PaddingLR: 2;
    Columns: [1.5 inch];
    Row {
      Background: Colors.DarkGray;
      TextColor: Colors.White;
      Lang.Name;
    };
    Edge: 0.25 pts {Color: Colors.DarkGray};
    LanguageRow(each list)
  };

  HeaderCell(d, halign=HAligns.Left) = Cell {
    HAlign: halign;
    VAlign: VAligns.Center;
    Style.SansSerif;
    Style.TitleBackground;
    EdgeB: 1 pts;
    Padding: 2 pts;
    d
  };

  TitleBar(name) = Paragraph {
    Background: 40%;
    TextHeight: 16 pts;
    name
  };

  BulletList = ListBlock {
    Marker: Tex.bullet;
    LeftIndent: 24 pts;
    FirstIndent: -16 pts;
  };

  NumberList = ListBlock {
    Marker: i => (i+1) ".";
    LeftIndent: 24 pts;
    FirstIndent: -16 pts;
  };

  BodyPar = Paragraph {
    TextHeight: 10.5 pts;
    ParAlignment: ParAlignments.Justify;
    SpaceBefore: 6 pts;
  };

  BodyTable = Table {
    SpaceBefore: 10 pts;
    LeftIndent: 0.5 inches;
  };

  BodyTitle = Span {
    Bold;
  };

  NeedsReview(text) = InParens {
    TextColor: Colors.Red;
    text;
  };

  FigureBody = Paragraph {
    KeepWithNext: true;
    ParAlignment: ParAlignments.Center;
    SpaceBefore: 6 pts;
  };

  Equation = HBox {
    TextEquation: true;
  };

  EquationPar = Paragraph {
    SpaceBefore: 10 pts;
    TextEquation: true;
    TextStacked: true;
    ParAlignment: ParAlignments.Center;
  };

  FigureLabel = Paragraph {
    TextHeight: 12 pts;
    ParAlignment: ParAlignments.Center;
    SpaceBefore: 6 pts;
  };

  FigureName(figure) = Span {
    Lang.Figure;
    Space;
    figure.Number;
  };

  DocumentFigure(figure, textheight=null) = Block {
    FigureBody {
      TextHeight: textheight;
      figure.Body;
    };

    FigureLabel {
      TextHeight: 10 pts;
      FigureName(figure);
      ": ";
      figure.Caption;
    };
  };

  SlideFigure(figure, textheight=null) = Block {
    Paragraph {
      figure.Caption;
    };
    Block {
      ParAlignment: ParAlignments.Center;
      TextHeight: textheight;
      figure.Body;
    }
  };
}
//======================================================================

abstract class BaseViewClass: DocumentViewClass {
  Constructor(title, key, type) {
    super.Constructor(title, key, type);
  }

  FixedFrame(x, y, width, height) = Frame {
    HRelative: HRelatives.Page;
    VRelative: VRelatives.Page;
    Wrapping: Wrappings.BehindText;
    X: x;
    Y: y;
    Width: width;
    Height: height;
  };

  Design(width, height, right, bar) = Canvas {
    Height: height;

    var x0 = 0;
    var y0 = 0;
    var t  = 0.10 height;
    var x1 = 0.80 width;
    var x2 = 0.90 width;
    var p1 = Point((x2 + x1)*0.5, height * (right ? 0.25 : 0.75));

    Figure {
      Fill: bar;
      FigurePath {
        Closed: true;
        LineTo(x0, t);
        LineTo(x1, t);
        QuadTo(p1, Point(x2, height));
        LineTo(x2, height);
        LineTo(x0, height);
      }
    };

/*
    Figure {
      Fill: Style.RightBar;
      FigurePath {
        Closed: true;
        LineTo(x1, y0);
        LineTo(width, y0);
        LineTo(width, height-t);
        LineTo(x2, height-t);
        QuadTo(p1, Point(x1, y0));
      }
    };
*/
    Figure {
      Fill: Style.BarLine;
      FigurePath {
        Closed: true;
        MoveTo(x0, y0);
        LineTo(x1, y0);
        QuadTo(p1, Point(x2, height-t));
        LineTo(width, height-t);
        LineTo(width, height);
        LineTo(x2, height);
        QuadTo(p1, Point(x1, t));
        LineTo(x0, t);
      }
    };
  };

  HeaderBackground(title, width, height) = Design(width, height, true, Style.HeaderBar1..Style.HeaderBar2) {
    Frame {
      Width: 0.75 width;
      VAlignment: VAligns.Center;
      X: DocMetrics.PageMargin.Left;
      Paragraph {
        TextHeight: 30 pts;
        TextColor: Colors.White;
        title;
      };
    };
  };

  FooterBackground(title, width, height, page) = Design(width, height, false, Style.LeftBar) {
    Frame {
      VAlignment: VAligns.Center;
      X: DocMetrics.PageMargin.Left;
      Width: DocMetrics.PageContent.Width;
      Paragraph {
        TabStops: [(DocMetrics.PageContent.Width) {Type: TabTypes.Right}];
        TextHeight: 16 pts;
        Span {
          TextColor: 75%;
          title
        };
        Tab;
        Span {
          TextColor: Style.HeaderBar1;
          page;
        }
      }
    }
  };

  AddSlideBlock(slide, page) = Block {
    Paragraph {
      PageBreakBefore: true;
      FixedFrame(0, 0, DocMetrics.DocumentSize.Width, DocMetrics.SlideHeader) {
        HeaderBackground(slide.Title, DocMetrics.DocumentSize.Width, DocMetrics.SlideHeader)
      };
      FixedFrame(0, DocMetrics.DocumentSize.Height - DocMetrics.SlideFooter, DocMetrics.DocumentSize.Width, DocMetrics.SlideFooter) {
        FooterBackground(ViewInfo.Title, DocMetrics.DocumentSize.Width, DocMetrics.SlideFooter, page);
      };
    };
    slide.Body;
  };
}
//======================================================================

with Graphics {
  PI            = Real(Math.PI); // Floating point number for speed
  Half          = 0.5;
  NinetyDegrees = Half * PI;
  FortyFive     = 0.25 * PI;

  ProjectAngle(center, r, angle) {
    return Point(center.X + r * Cos(angle), center.Y + r * Sin(angle));
  }

  Adjust(center, r, angle) {
    return center - ProjectAngle(Point(0), r, angle);
  }

  GetAngle(p1, p2) {
    var d      = p1 - p2;
    var angle  = NinetyDegrees;
    if (d.X != 0) {
      angle = ArcTan(d.Y / d.X);
      if (d.X < 0)
        angle -= PI;
    }
    return angle;
  }

  TwoArrowLine(p1, p2, size) = Figure {
    Fill: Colors.Black;

    var flat  = size;
    var tip   = Sqrt(2*(size*Half + flat)^2);
    var angle = GetAngle(p1, p2);
    var opp   = angle - NinetyDegrees;
    var a1    = ProjectAngle(p1, tip, angle + 1.5 * NinetyDegrees);
    var a2    = ProjectAngle(a1, flat, opp);
    var a3    = ProjectAngle(a2, size, opp);
    var b1    = ProjectAngle(p2, tip, angle + FortyFive);
    var b2    = ProjectAngle(b1, flat, opp);
    var b3    = ProjectAngle(b2, size, opp);

    FigurePath {
      Closed: true;
      p1;

      LineTo(a1);
      LineTo(a2);
      LineTo(b2);
      LineTo(b1);
      LineTo(p2);
      LineTo(ProjectAngle(b3, flat, opp));
      LineTo(b3);
      LineTo(a3);
      LineTo(ProjectAngle(a3, flat, opp));
    }
  };

  CircleArrow(center, radius, angle, anglegap, size) = Figure {
    Fill: Colors.Black;

    var angle1 = angle + anglegap;
    var angle2 = angle - anglegap;
    var an1    = angle1 - NinetyDegrees;
    var an2    = angle2 - NinetyDegrees;
    var flat   = size;
    var tip    = Sqrt(2*(size*Half + flat)^2);
    var p1     = Graphics.ProjectAngle(center, radius, angle1);
    var p2     = Graphics.ProjectAngle(center, radius, angle2);
    var a1     = ProjectAngle(p1, tip, an1 + 1.5 * NinetyDegrees);
    var a2     = ProjectAngle(a1, flat, an1 - NinetyDegrees);
    var a3     = ProjectAngle(a2, size, an1 - NinetyDegrees);
    var b1     = ProjectAngle(p2, tip, an2 + FortyFive);
    var b2     = ProjectAngle(b1, flat, an2 - NinetyDegrees);
    var b3     = ProjectAngle(b2, size, an2 - NinetyDegrees);


    FigurePath {
      Closed: true;
      p1;
      LineTo(a1);
      LineTo(a2);
      ArcTo(b2, radius + size, angle, true, true);
      LineTo(b1);
      LineTo(p2);
      LineTo(ProjectAngle(b3, flat, an2 - NinetyDegrees));
      LineTo(b3);
      ArcTo(a3, radius, angle, true);
      LineTo(ProjectAngle(a3, flat, an1 - NinetyDegrees));
    };
  };

}
//======================================================================

