using Format, Units, Math, IO, LinkTypes;

//======================================================================

class AlignDataClass {
  var Prob,
      WordSet;

  Constructor(prob, wordset) {
    Prob    = prob;
    WordSet = wordset;
  }

  override GetLayoutTip = Span {
    TextDigits: 2;
    "{0}% probability"(Prob*100);
  };
}

//======================================================================

class AlignIndexClass {
  var              ConceptName,
                   CognateName;
  CognateClass     Cognate;
  AlignDataClass[] Alignments;

  Constructor(concept, cognate, AlignDataClass[] alignments) {
    ConceptName = concept;
    CognateName = cognate;
    Alignments  = alignments;
  }
}

//======================================================================

class StatClass {
  var Lower,
      Mean,
      Upper;

  Constructor(lower, mean, upper) {
    Lower = lower;
    Mean  = mean;
    Upper = upper;
  }

  ShowRight(number) = HBox {
    Space*2;
    TextHeight: 80%;
    TextColor: Colors.Gray;
    number;
  };

  ShowBox = VBox {
    Brackets: Brackets.Curly;
    TextDigits: 3;
    ShowRight(Upper * 100.0);
    Mean * 100.0;
    ShowRight(Lower * 100.0);
  };

  override GetLayoutTip = ShowBox;
}

//======================================================================

class AlignPairClass {
  DataSetClass    Data;
  AlignIndexClass Cognate;
  SizeClass       BoxSize;
  var             Name;

  Constructor(DataSetClass data, AlignIndexClass cognate) {
    Data    = data;
    Cognate = cognate;
    BoxSize = new SizeClass(14 pts, 14 pts);
    Name    = cognate.ConceptName;
    if (cognate.CognateName != DefaultCognate)
      Name += "-" + cognate.CognateName;
  }

  Languages(data, height) = VBox {
    HAlign: HAligns.Right;
    HBox(null, height);

    foreach (var taxon in data.Taxa) {
      HBox(null, height) {
        VAlign: VAligns.Center;
        taxon.Language;
      };
    }
  };

  ShowBlock(prob, cumulative, wordset, boxheight, digits) = VBox {
    Margin: PadB(10 pts);
    Border: new EdgeClass(0.125, 50%);
    HAlign: HAligns.Center;

    HBox(null, boxheight) {
      TextDigits: digits;
      prob * 100.0;
      "%";
    };

    VBox {
      foreach (var taxon in Data.Taxa) {
        HBox(null, boxheight) {
          var word = wordset[each0];
          if (word is valid) {
            foreach (var c in word) {
              Canvas(BoxSize.Width, BoxSize.Height) {
                HAlign: HAligns.Center;
                SegPartClass part;
                if (c == -1)
                  part = IPAParts.Gap;
                else if (c >= 0 and c < Data.UniqueSegments.Length) {
                  part = Data.UniqueSegments[c];
                  Background: part.Partition.Color;
                  TextColor: part.Partition.TextColor;
                }
                else
                  part = IPAParts.Missing;
                part.ShowSegment;
              };
            }
          }
        };
      }
    };
  };

  override GetLayoutTip = ShowDetails(9, true, 10 pts, 1);

  override GetDocument = ShowDetails(5000, true, 10 pts, 3);

  GetChart(SizeClass size, max) = Chart(size.Width, size.Height) {
    Type: ChartTypes.Column;
    TextHeight: 6 pts;
    ValueLabel: "%";
    ChartSeries {
      foreach (var a in Cognate.Alignments) {
        new DataPointClass(each0, a.Prob * 100.0) {Reference: a};
        if (each1 >= max)
          break;
      }
    };
  };

  ShowDetails(max, tip, textheight, int digits) = Block {
    TextHeight: textheight;

    Paragraph {
      KeepWithNext: true;
      TextHeight: textheight + 4 pts;
      SpaceAfter: 10 pts;

      TabStops: [3 inches];
      Name;
    };

    if (Cognate.Alignments.Length > 1)
      GetChart(new(3 inch, 0.75 inch), max);

    var langbox = Languages(Data, BoxSize.Height);
    var indent  = GetLayoutSize(langbox).Width;
    Paragraph {
      if (tip) {
        langbox;
        Space;
      }
      else {
        LeftIndent: indent;
        FirstIndent: -indent;
        Tab;
        langbox;
      }

      ShowBlocks(max, BoxSize.Height, digits);
    }
  };

  ShowBlocks(max, boxheight, int digits) = Span {
    var cumulative = 0.0;
    foreach (var a in Cognate.Alignments) {
      var prob = a.Prob;
      cumulative += prob;
      var wordset = a.WordSet;
      if (wordset.Length > 0) {
        ShowBlock(prob, cumulative, wordset, boxheight, digits);
        Space;
        if (each1 >= max)
          break;
      }
    }
  };

  ShowFigureDetails(max) = Block {
    var h = 8 pts;
    BoxSize = new(BoxSize.Width, h);
    TextHeight: 7 pts;
    Span {
      Languages(Data, h);
      ShowBlocks(max, h, 1);
    }
  };

  ShowFigureData(AlignIndexClass index, AlignDataClass align) = Frame {
    var h = 8 pts;
    BoxSize = new(BoxSize.Width, h);
    Paragraph {
      SpaceBefore: 6 pts;
      TextHeight: 12 pts;
      index.Cognate.GetTitle;
    };

    Paragraph {
      TextHeight: 7 pts;
      Span {
        Languages(Data, h);
        ShowBlock(align.Prob, align.Prob, align.WordSet, h, 1);
      }
    };
  };
}

//======================================================================

class CognateIdClass {
  var Concept,
      Cognate,
      Count;

  Constructor(concept, cognate, count) {
    Concept = concept;
    Cognate = cognate ?? DefaultCognate;
    Count   = count;
  }
}

//======================================================================

class AlignmentChartClass {
  ResultsClass Results;

 Constructor(ResultsClass results) {
    Results = results;
  }

  ShowChart(SizeClass size) = VBox(size.Width, size.Height) {
    Style.SansSerif;
    Border: 0.5;

    // Try to make proportional squares
    var xitems = size.Width / 130 as int;
    var yitems = Math.Floor((Results.Alignments.Length + xitems - 1) / xitems);
    var bsize  = new SizeClass(size.Width / xitems, size.Height / yitems);
    var x      = bsize.Width * 0.5;
    AlignIndexClass[][] rows = Results.Alignments / xitems;

    TextHeight: Math.Min(9 pts, bsize.Height - 3.2 pt);

    foreach (var row in rows) {
      HBox {
        foreach (var a in row) {
          VBox(bsize.Width, bsize.Height) {
            HAlign: HAligns.Center;
            Border: new EdgeClass(0.25 pts, 90%, 1);

            var ap = new AlignPairClass(Results.Data, a);
            Paragraph {
              Span {
                ap.Name;
                TipAction: new ActionClass(Actions.TipDocument, ap);
              };
            };
            Paragraph {
              TextColor: Colors.LightGray;
              a.Alignments.Length;
            };
          }
        }
      }
    }
  };

  ShowAll = Block {
    foreach (var a in Results.Alignments) {
      new AlignPairClass(Results.Data, a).ShowDetails(5, false, 10 pts, 3);
      Paragraph {
        Border: BorderT(1);
      };
    }
  };

  ShowSet(DataSetClass data, CognateIdClass[] examples) = MatrixBlock {
    var alignments = Results.Alignments;
    var chartwidth = 1 inch;

    foreach (var e in examples) {
      var found = null;
      foreach (var a in alignments) {
        if (a.ConceptName == e.Concept && a.CognateName == e.Cognate) {
          found = a;
          break;
        }
      }

      if (found is valid) {
        var ap = new AlignPairClass(data, found);
        Row {
          VAlign: VAligns.Top;
          Frame(0.75 inch, null, PadR(10 pts)) {
            e.Concept;
            e.Cognate Italic;
          };
          ap.GetChart(new(chartwidth, data.Taxa.Length * (9 pts)), e.Count);
          Frame(5 inches) {
            ap.ShowFigureDetails(e.Count);
          }
        }
      }
    }
  };
}

//======================================================================

class LanguageClass: FactClass {
  field string Name;
  field int    Cases;
  int          LangIndex;
  var          MapAttribution;

  Constructor(id, name, cases=0) {
    super.Constructor(id);
    Name  = name;
    Cases = cases;
  }

  override IconClass GetIcon = Icons.language;

  virtual Description = null;
  virtual Details = null;

  override GetLayoutSpan = Span {
    TipAction: this;
    Name;
  };

  virtual MapImage(SizeClass size) = null;

  override GetLayoutTip = Block {
    Paragraph {
      Bold;
      TextHeight: 14 pts;
      Name;
    };
    Description;

    ShowMap(new(4 inches, 2 inches));
  };

  static ImageClass LoadImage(name) = new ImageClass(Info.ImageFolder FileName(name));

  ShowMap(SizeClass size) {
    var h     = 6 pts;
    var image = MapImage(new(size.Width, size.Height - h*2));
    if (image is valid) {
      return HBox {
        HAlign: HAligns.Center;

        VBox {
          image;

          if (MapAttribution is valid) {
            Frame(size.Width) {
              TextHeight: h;
              TextColor: 80%;
              Lang.Credit;
              MapAttribution;
            }
          }
        }
      };
    }
    else
      return null;
  }
}

//======================================================================

class WordClass: FactClass {
  field string   Text;
  LanguageClass  Language;
  TaxonClass     Taxon;
  CognateClass   Cognate;
  SegPartClass[] Segments,
                 Aligned,
                 Padded;
  var            Variable;

  Constructor(id, text, TaxonClass taxon, CognateClass cognate) {
    super.Constructor(guid.FromHash(id));
    Text     = text;
    Taxon    = taxon;
    Language = taxon.Language;
    Cognate  = cognate;
  }

  void Init {
    Segments = [];
    Aligned  = [];
  }

  ShowPlain = Span {
    Action: new ActionClass(Actions.ToSource, Variable);
    foreach (var c in Text) {
      if (c != IPASegments.GapSegment.Ipa and c != IPASegments.PrimaryStress.Ipa)
        c;
    }
  };

  void InitRaw(variable, SegPartDictionary lookup) {
    // IPA text and word text must be in the same unicode normaization form so comparisons are accurate
    Variable = variable;

    if (Text is valid) {
      Text = (Text as string).Normalize(NormalizationForms.FormC);

      var segments = lookup.FindTokens(Text, IPA.NullSegment);
      Aligned  = segments.FindSlice(s => s.Segment.Gap or (not s.Punctuation));
      Segments = segments.FindSlice(s => not s.Punctuation);

      // Test that the word's text is the same as the segments converted back into text
      var view = Span {(each segments).Ipa};
      System.Assert(Text == (view as string), "Round-trip error with text {0}-{1}"(Text, view), Text);
      Connect;
    }
    else
      Init;
  }

  void InitExperiment(variable, plain, SegPartClass[] segments) {
    Variable  = variable;
    Aligned   = segments;
    Padded    = segments;
    Segments  = segments.FindSlice(s => not s.Punctuation);
    Connect;
  }

  void Connect {
    Link.Connect(this, Segments);
    Link.Connect(this, Cognate);
    Link.Connect(this, Language);
    Link.Connect(Language, Cognate);
  }

  override IconClass GetIcon = Icons.text_snippet;

  ShowConcept = Span {
    Style.SansSerif;
    TipAction: this;
    Cognate.GetTitle;
  };

  override GetLayoutSpan = Span {
    foreach (var s in Segments) {
      if (!s.Punctuation)
        s.Ipa;
    }
  };

  ShowPaddedBlocks(textheight, index=-1) = HBox {
    TextHeight: textheight;

    foreach (var segment in Padded) {
      HBox(textheight*1.125) {
        Border: BorderL(new(0.125 pts, 80%));
        HAlign: HAligns.Center;
        if (index == each0)
          Background: Colors.LightYellow;
        segment.ShowSegment;
      };
    }
  };

  FieldPar(name) = Paragraph {
    TabStops: [0.5 inches];
    Bold name;
    Tab;
  };

  override GetLayoutTip = Block {
    FieldPar(Lang.Language) {
      Language;
    };
    FieldPar(Lang.Cognate) {
      Cognate.GetTitle;
    };
    FieldPar(Lang.Segments) {
      Frame {
        Border: 0.5;
        ShowPaddedBlocks(16 pts);
      }
    }
  };

  override GetLayoutFact = Block {
    Paragraph {
      TabStops: [1 inch, 2, inch, 3 inch, 4 inch];
      GetIcon;
      Space;
      Quote;
      ShowPlain;
      Quote;
      Tab;
      Language;
      ColonSpace;
      Cognate.GetTitle;
    };
    super.GetLayoutFact;
  };
}

//======================================================================

class ConceptClass: FactClass {
  field string Name;
  field double Confidence;
  var          Variable;
  WordTypes    WordType;
  WordGroups   WordGroup;

  Constructor(variable) {
    super.Constructor(guid.FromHash(variable.GetVariable.FullName));
    Variable = variable;
    Name     = variable.GetVariable.Name;
  }

  void Check {
    Confidence = Variable.Confidence;
    WordType   = Variable.WordType;
    WordGroup  = Variable.WordGroup;

    System.Assert(Confidence > 0);
    System.Assert(WordType != WordTypes.None);
    System.Assert(WordGroup != WordGroups.None);
  }

  override IconClass GetIcon = Icons.category;

  override GetLayoutTip = Span {Name; "-"; Confidence};

  override Compare(c, CompareOptions options) {
    return Name.Compare(c.Name);
  }
}
//======================================================================

class CognateClass: FactClass {
  field string Name;
  ConceptClass Concept;
  WordClass[]  Words;
  var          MaxLength,
               Variable;

  Constructor(ConceptClass concept, variable) {
    super.Constructor(guid.FromHash(variable.GetVariable.FullName));

    Name      = variable.GetVariable.Name;
    Variable  = variable;
    Concept   = concept;
    MaxLength = 0;

    Link.Connect(this, concept);
  }

  override IconClass GetIcon = Icons.format_list_bulleted;

  GetSource = Concept.Name + '-' + Name;

  GetTitle = Span {
    Concept.Name; '-'; Name;
    TipAction: this;
  };

//  override GetLayoutTip = AllWords.ShowConceptTable(Data, this);

  override Compare(c, CompareOptions options) {
    var i = Concept.Compare(c.Concept, options);
    if (i == 0)
      return Name.Compare(c.Name, options);
    return i;
  }
}
//======================================================================

class TaxonClass {
  var           Variable,
                Index,
                WordCount,
                SegmentCount;
  LanguageClass Language;

  Constructor(LanguageClass language) {
    Index        = each0;
    Language     = language;
    Variable     = language.GetVariable.Name;
    WordCount    = 0;
    SegmentCount = 0
  }

  override GetLayoutTip = Span {
    Style.TaxonFormat;
    Variable;
    TipAction: Language;
  };
}
//======================================================================

class WordPairClass {
  var       Index;
  WordClass Word1,
            Word2;

  Constructor(index, WordClass w1, WordClass w2) {
    Index = index;
    Word1 = w1;
    Word2 = w2;
  }

  ShowDescription(textheight) = Frame {
    Border: 0.25 pts;
    TextHeight: textheight;
    Paragraph {
      ParBackground: 90%;
      Bold;
      Word1.Cognate.Variable.GetVariable.FullName[9..];
    };

    Word1.ShowPaddedBlocks(textheight, Index);
    Word2.ShowPaddedBlocks(textheight, Index);
  };

  override GetLayoutTip = ShowDescription(12 pts);
}

//======================================================================

class DataSetClass {
  LanguageClass[]             LanguageSet;
  SegPartClass[]              UniqueSegments;
  CognateClass[]              UsedCognates,
                              SortedCognates;
  ConceptClass[]              UsedConcepts;
  TaxonClass[]                Taxa;
  DictionaryClass<TaxonClass> TaxonSet;
  VectorClass                 WordList;
  RuleClass                   Rule;
  WordClass                   DefWord;
  TreeSetClass[]              TreeSets;
  ListClass                   PlainBuffer,
                              TextBuffer;
  ListClass<SegPartClass>     SegmentBuffer;
  RunExperimentClass          MainRun;
  var                         WordCutoff,
                              RootFolder,
                              Name,
                              MaxLanguageName,
                              WordCount,
                              SegmentCount,
                              ExampleConcept,
                              FigureConcept,
                              WordFile,
                              DataConcept;

  Constructor(config, name, rootfolder, runindex) {
    Name            = name;
    RootFolder      = Folder(rootfolder);
    WordFile        = RootFolder Info.WordFileName;
    WordCount       = 0;
    SegmentCount    = 0;
    MaxLanguageName = 0;
    TextBuffer      = new(64);
    PlainBuffer     = new(64);
    SegmentBuffer   = new(64);
    Rule            = new NaturalPartitionClass;
    TreeSets        = AddTree(config, each PriorTrees);
    MainRun         = TreeSets[0].Custom.Run[runindex];

    if (config is null)
      ReadExperiment;
  }

  TreeSetClass AddTree(config, NodeClass root) = new(config, "Tree"+each1, RootFolder, root, Rule);

  void ReadRawWords(concepts, includewords, LanguageClass[] languageset, cutoff=-1) {
    if (cutoff < 0)
      cutoff = languageset.Length;
    WordCutoff = cutoff;

    AddLanguageSet(languageset);

    var cognatelist = new ListClass<CognateClass>(1024);
    var conceptlist = new ListClass<ConceptClass>(256);

    foreach (var conceptvar in concepts) {
      var good = exists conceptvar.?Confidence and
                 exists conceptvar.?WordType and
                 exists conceptvar.?WordGroup;
      System.Assert(good, "Improper concept", conceptvar);

      if (good and conceptvar.Confidence > 0 and conceptvar.WordGroup.HasFlag(includewords)) {
        foreach (var cognate in conceptvar) {
          var vname = cognate.GetVariable.Name;
          var inc   = true;

          switch (vname) {
            case "Confidence":
            case "WordType":
            case "WordGroup":
              inc = false;
              break;
          }

          if (inc) {
            var count = 0;
            foreach (var word in cognate) {
              if (word is valid) {
                var taxon = TaxonSet.FindKey(word.GetVariable.Name);
                if (taxon is valid)
                  ++count;
              }
            }

            if (count >= cutoff) {
              var concept = new ConceptClass(conceptvar);
              concept.Check;
              conceptlist.Add(concept);
              cognatelist.Add(new(concept, cognate));
            }
          }
        }
      }
    }


    UsedConcepts = conceptlist.Values.Sort(false);
    UsedCognates = cognatelist.Values;

    var lookup = new SegPartDictionary(IPASegments.Length);
    foreach (var segment in IPASegments) {
      if (segment.Ipa is valid)
        lookup.Add(new(segment), segment.Ipa);
    }

    foreach (var cognate in UsedCognates)
      ComputeRawCognate(lookup, cognate);

    var set = new SegmentSetClass(512);
    foreach (var cognate in UsedCognates) {
      foreach (var word in cognate.Words) {
        foreach (var segment in word.Segments) {
          if (!segment.Punctuation)
            set.Add(segment);
        }
      }
    }
    UniqueSegments = set.Values;

    GetConceptExamples(concepts);
    CompletedRead;
  }

  void ReadExperiment {
    var run = ReadScript(Info.ExperimentRoot WordFile).Root;

    WordCutoff  = run.Info.WordCutoff;
    AddLanguageSet(run.LanguageSet);

    var slist  = new ListClass<SegPartClass>(run.Segments.Length);
    var lookup = new SegmentDictionary(IPASegments.Length);
    foreach (var segment in IPASegments) {
      if (segment.Ipa is valid)
        lookup.Add(segment, segment.Ipa);
    }

    foreach (var text in run.Segments) {
      var s = lookup.FindKey(text);
      if (s is valid)
        slist.Add(new(s));
      else
        System.Assert(false, "Missing Segment " + text);
    }

    UniqueSegments = slist.Values;

    var concepts    = run.Concepts;
    var conceptlist = new ListClass<ConceptClass>(concepts.Length);
    var cognatelist = new ListClass<CognateClass>(concepts.Length * 2);

    foreach (var conceptvar in concepts) {
      var concept = new ConceptClass(conceptvar);
      conceptlist.Add(concept);

      foreach (var cognatevar in conceptvar) {
        var cognate = new CognateClass(concept, cognatevar);
        cognatelist.Add(cognate);
        ComputeExperimentCognate(concept, cognate, cognatevar);
      }
    }

    UsedConcepts = conceptlist.Values;
    UsedCognates = cognatelist.Values;

    GetConceptExamples(concepts);
    CompletedRead;
  }

  void AddLanguageSet(languageset) {
    LanguageSet = languageset;
    TaxonSet    = new(LanguageSet.Length);

    ListClass<TaxonClass> tlist = new(LanguageSet.Length);

    foreach (var l in LanguageSet) {
      l.LangIndex = each0;

      var taxon = new TaxonClass(l);
      tlist.Add(taxon);
      TaxonSet.Add(taxon, taxon.Variable);
      if (taxon.Variable.Length > MaxLanguageName)
        MaxLanguageName = taxon.Variable.Length;
    }
    Taxa     = tlist.Values;
    WordList = new(Taxa.Length, null);
    DefWord  = new WordClass(#71f32657-a37c-4f1d-abba-87b928e9d80b#, Empty, Taxa[0], null);
    DefWord.Init;
  }

  virtual void GetConceptExamples(concepts) {
    ExampleConcept = concepts.Stone;
    FigureConcept  = concepts.What;
    DataConcept    = concepts.Mountain;
  }

  void CompletedRead {
    var factor = 1.0 / SegmentCount;
    foreach (var segment in UniqueSegments) {
      segment.Index         = each0;
      segment.UsedFrequency = factor * segment.UsedCount;
    }

    Rule.Init(UniqueSegments);
  }

  Coverage(TaxonClass taxon) {
    var index = taxon.Index;
    var count = 0.0;
    var used  = 0.0;
    foreach (var c in UsedCognates) {
      var word = c.Words[index];
      if (word is valid and word.Text is valid)
        ++used;

      ++count;
    }
    return used / count;
  }

  CoverageByLanguages {
    var count = new VectorClass(Taxa.Length+1, 0.0);
    foreach (var c in UsedCognates) {
      var used = 0;
      foreach (var word in c.Words) {
        if (word is valid and word.Text is valid)
          ++used;
      }
      count.AddValue(used, 1.0);
    }
    return count;
  }

  CognateClass[] GetSortedCognates {
    SortedCognates ??= UsedCognates.Sort(false);
    return SortedCognates;
  }

  CognateClass GetCognate(concept) = UsedCognates.FindFirst(c => c.Concept.Variable == concept);

  LangNumber(LanguageClass language) = Languages.FindFirstIndex(language)+1;

  void ComputeRawCognate(SegPartDictionary lookup, CognateClass cognate) {
    WordList.SetAllValues(DefWord);
    var maxlength = 0;
    foreach (var text in cognate.Variable) {
      var taxon = TaxonSet.FindKey(text.GetVariable.Name);
      if (taxon is valid) {
        var word = new WordClass(text+cognate.Concept.Name+cognate.Name+taxon.Variable, text, taxon, cognate);
        word.InitRaw(text, lookup);
        maxlength = Math.Max(maxlength, word.Aligned.Length);

        int count = 0;
        foreach (var s in word.Segments) {
          if (s.Index >= 0)
            ++count;
        }

        SegmentCount       += count;
        taxon.SegmentCount += count;
        WordList.SetValue(taxon.Index, word);
      }
    }

    cognate.Words     = WordList.Values;
    cognate.MaxLength = maxlength;

    foreach (var word in cognate.Words) {
      var a = word.Aligned;
      if (a is valid) {
        if (a.Length < maxlength)
          word.Padded = a + [IPAParts.Gap] * (maxlength - a.Length);
        else
          word.Padded = a;
      }
      else
        word.Padded = a;
    }

    for (var i = 0; i < maxlength; ++i) {
      var count    = 0;
      var gapcount = 0;

      foreach (var word in cognate.Words) {
        if (word.Aligned is valid) {
          ++count;
          if (word.Padded[i] == IPAParts.Gap)
            ++gapcount;
        }
      }
      System.Assert(gapcount < count, "Column with all gaps", cognate.Variable);
    }
  }

  void ComputeExperimentCognate(ConceptClass concept, CognateClass cognate, cognatevar) {
    WordList.SetAllValues(DefWord);

    var maxlength = 0;


    foreach (var word in cognatevar) {
      if (word is valid) {
        var taxon = Taxa[each0];
        if (taxon is valid) {
          ++taxon.WordCount;
          ++WordCount;

          TextBuffer.Clear();
          PlainBuffer.Clear();
          SegmentBuffer.Clear();

          var gs    = IPAParts.Gap;
          var gap   = gs.Ipa[0];
          var count = 0;
          foreach (var index in word) {
            if (index < 0) {
              TextBuffer.Add(gap);
              SegmentBuffer.Add(gs);
            }
            else {
              var s = UniqueSegments[index];
              SegmentBuffer.Add(s);
              foreach (var c in s.Ipa) {
                TextBuffer.Add(c);
                PlainBuffer.Add(c);
              }
              ++s.UsedCount;
              ++count;
            }
          }

          SegmentCount       += count;
          taxon.SegmentCount += count;


          var text = TextBuffer.Values;
          var w    = new WordClass(text+cognate.Concept.Name+cognate.Name+taxon.Variable, text, taxon, cognate);
          w.InitExperiment(word, PlainBuffer.Values, SegmentBuffer.Values);
          if (w.Aligned.Length > maxlength)
            maxlength = w.Aligned.Length;
          WordList.SetValue(taxon.Index, w);
        }
      }
    }

    cognate.Words     = WordList.Values;
    cognate.MaxLength = maxlength;
  }

  ShowWord(SegmentClass segment) = Block {
    Paragraph {
      SpaceBefore: 10 pts;
      TextHeight: 14;
      Border: BorderB(1);
      "Words containing \"{0}\""(segment.Ipa);
    };
    Paragraph {
      Separator: Space;
      foreach (var cognate in UsedCognates) {
        foreach (var word in cognate.Words) {
          if (word.Segments.FindAny(segment))
            word.ShowPlain;
        }
      }
    };
  };

  virtual NodeClass PortugueseClade    = new Leaf(Languages.Portuguese);
  virtual NodeClass PortugueseNumbered = new NLeaf(this, Languages.Portuguese, 2);

  CatalanClade = new Branch {
    new Leaf(Languages.Catalan);
    new Branch {
      new Leaf(Languages.French);
      new Leaf(Languages.Walloon);
    };
  };

  NodeClass NumberedTree = new NBranch(19) {
    new NLeaf(this, Languages.Latin, 7);
    new NBranch(18) {
      new NLeaf(this, Languages.Romanian, 6);
      new NBranch(17) {
        new NBranch(16) {
          new NBranch(15) {
            new NLeaf(this, Languages.Walloon, 3);
            new NLeaf(this, Languages.French, 3);
          };
          new NBranch(14) {
            new NBranch(13) {
              PortugueseNumbered;
              new NLeaf(this, Languages.Spanish, 2);
            };
            new NLeaf(this, Languages.Catalan, 3);
          }
        };
        new NBranch(11) {
          new NLeaf(this, Languages.Friulian, 4);
          new NLeaf(this, Languages.Italian, 4);
        };
      }
    }
  };

/*
  NodeClass Tree0 = new Branch {
    new Leaf(Languages.Latin);
    new Branch {
      new Leaf(Languages.Italian);
      new Branch {
        new Branch {
          new Leaf(Languages.Spanish);
          PortugueseLeaf;
        };
        new Branch {
          new Branch {
            new Leaf(Languages.Friulian);
            CatalanClade;
          };
          new Leaf(Languages.Romanian);
        };
      }
    }
  };
*/

  virtual NodeClass[] PriorTrees = [
    // (Latin,(Romanian,(Italian,(Friulian,((Spanish,Portuguese),(Catalan,(French,Walloon)))))));
    new Branch {
      new Leaf(Languages.Latin);
      new Branch {
        new Leaf(Languages.Romanian);
        new Branch {
          new Leaf(Languages.Italian);
          new Branch {
            new Leaf(Languages.Friulian);
            new Branch {
              new Branch {
                new Leaf(Languages.Spanish);
                PortugueseClade;
              };
              CatalanClade;
            };
          }
        }
      }
    },

    // (Latin,(Romanian,((Italian,Friulian),((Spanish,Portuguese),(Catalan,(French,Walloon))))));
    new Branch {
      new Leaf(Languages.Latin);
      new Branch {
        new Leaf(Languages.Romanian);
        new Branch {
          new Branch {
            new Leaf(Languages.Italian);
            new Leaf(Languages.Friulian);
          };
          new Branch {
            new Branch {
              new Leaf(Languages.Spanish);
              PortugueseClade;
            };
            CatalanClade;
          };
        };
      };
    },

    // (Latin,(Romanian,((Spanish,Portuguese),((Catalan,(French,Walloon)),(Italian,Friulian)))));
    new Branch {
      new Leaf(Languages.Latin);
      new Branch {
        new Leaf(Languages.Romanian);
        new Branch {
          new Branch {
            new Leaf(Languages.Spanish);
            PortugueseClade;
          };
          new Branch {
            CatalanClade;
            new Branch {
              new Leaf(Languages.Italian);
              new Leaf(Languages.Friulian);
            };
          };
        };
      };
    },
  ];

  ShowTrees(ModelClass model) = Block {
    var any = false;
    foreach (var r in model.Run) {
      if (r.ResultTree is not null) {
        any = true;
        break;
      }
    }

    if (any) {
      Style.Header1 {
        Lang.Model;
        ColonSpace;
        model.Name;
      };

      foreach (var r in model.Run) {
        if (r.ResultTree is not null)
          Style.ShowTree("Run {0}"(each1), ExtentWidth, null, r.ResultTree, null);
      }
    }
  };

  ShowExperiment = Block {
    Style.Header1 {
      Lang.Experiment;
      Space;
      each1;
      Space;
      Style.DQuote {Name};
    };

    Span {
      foreach (var tree in PriorTrees)
        Style.ShowTree(Lang.Tree+each1, 30%, null, tree, null);
    };

    foreach (var treeset in TreeSets) {
      Style.Header2 {
        Lang.Tree;
        each1;
      };
      ShowTrees(each treeset.Models);
    }
  };

  override IconClass GetIcon = Icons.science;

  CalculateLoad(SegPartClass[] segments, sum) {
    return sum;
  }

  ShowFunctionalLoad = Block {
    Style.Header1 {
      "Functional Load of \"{0}\" model"(Rule.Name);
    };

    Table {
      Columns: [1.5 inch, 1 inch {HAlign: HAligns.Right}];
      Row {
        Lang.Partition;
        Lang.FrequencyPercent;
        Lang.FunctionalLoad;
      };

      foreach (var part in Rule.Partitions) {
        var sum = 0;//Math.Sum((each part.Segments).UsedFrequency);
        Row {
          part.Name;
          Cell {
            TextDigits: 2;
            sum;
          };
          Cell {
            TextDigits: 2;
            CalculateLoad(part.Segments, sum);
          }
        };
      }
    }
  };

}

//======================================================================

class ResultsClass {
  DataSetClass       Data;
  RuleClass          Rule;
  AlignIndexClass[]  Alignments;
  MatrixClass<StatClass> TransitionStats;
  RateMatrixClass    AverageRates,
                     QRates;
  var                AlignmentCutoff,
                     EquilibriumFrequencies;

  Constructor(DataSetClass data) {
    Data = data;
    Rule = data.Rule;


    var align = IO.ReadScript(data.MainRun.AlignmentFile).Root;

    QRates                 = new(align.QRates, false);
    AverageRates           = new(align.AverageRates, true);


    EquilibriumFrequencies = align.EquilibriumFrequencies;
    AlignmentCutoff        = align.AlignmentCutoff;
    Alignments             = align.Alignments;


    var ts = align?.TransitionStats;
    if (ts is not null)
      TransitionStats = Math.Matrix(ts);
    else {
      var length = QRates.Length;
      var bs = new StatClass(0, 0.5, 1);
      TransitionStats = new(length, length, bs, bs);
    }

    var alist = new ListClass<AlignIndexClass>(1024);
    foreach (var a in Alignments) {
      foreach (var c in Data.UsedCognates) {
        if (c.Concept.Name == a.ConceptName and c.Name == a.CognateName) {
          a.Cognate = c;
          alist.Add(a);
          break;
        }
      }
    }
    Alignments = alist.Values.Sort(false, ref CompareAlignment);
  }

  static CompareAlignment(AlignIndexClass a0, AlignIndexClass a1, options) {
    var c = a0.Alignments.Length.Compare(a1.Alignments.Length);
    if (c == 0)
      return a0.ConceptName.Compare(a1.ConceptName);
    return c;
  }
}

//======================================================================

abstract class CountBaseClass {
  var            VFlags,
                 MaxDigits;
  SegPartClass[] Segments;

  Constructor(DataSetClass data) {
    VFlags    = SegmentFeatures.Vowel | SegmentFeatures.Semivowel;
    Segments  = data.Rule.Segments;
    MaxDigits = 3;
  }

  PartitionBox = Paragraph {
    Style.SansSerif;

    var grid   = 0.125 pts;
    var gcolor = 65%;
    var border = 0.5 pts;
    var size   = ExtentSize - border*2;
    var length = Segments.Length;
    var d      = size / (length+1);

    Canvas {
      TextHeight: Math.Min(d.Height * 0.7, d.Width * 1.7 / MaxDigits);
      Border: border;

      var y = d.Height;
      for (var i = 0; i < length; ++i) {
        var x = d.Width;
        for (var j = 0; j < length; ++j) {
          AddEntry(Segments[i], Segments[j], x, y, d);
          x += d.Width;
        }
        y += d.Height;
      }

      y = d.Height;
      var p = 0;
      foreach (var s in Segments) {
        Canvas(d.Width, d.Height) {
          Y: y;
          Background: s.Partition.Color;
          TextColor: s.Partition.TextColor;
          HAlign: HAligns.Center;
          VAlign: VAligns.Center;
          s.ShowSegment;
        };
        if (p != s.PartitionIndex) {
          Canvas(size.Width, grid) {
            Y: y;
            Background: gcolor;
          };
          p = s.PartitionIndex;
        }
        y += d.Height;
      }

      Canvas(size.Width, d.Height) {
        var x = d.Width;
        p = 0;
        foreach (var s in Segments) {
          Canvas(d.Width, d.Height) {
            X : x;
            Background: s.Partition.Color;
            TextColor: s.Partition.TextColor;
            HAlign: HAligns.Center;
            VAlign: VAligns.Center;
            s.ShowSegment;
          };
          if (p != s.PartitionIndex) {
            Canvas(grid, size.Height) {
              X: x;
              Background: gcolor;
            };
            p = s.PartitionIndex;
          }
          x += d.Width;
        }
      };
    };
  };

  abstract AddEntry(SegPartClass si, SegPartClass sj, x, y, SizeClass size);

  Greenish(frac) {
    var f = (255.0 - frac * 255.0) as int;
    if (f < 255)
      return new ColorClass(f, 255, f);
    return null;
  }

  Redish(frac) {
    var f = (255.0 - frac * 255.0) as int;
    if (f < 255)
      return new ColorClass(255, f, f);
    return null;
  }
}

//======================================================================

class PriorCountClass: CountBaseClass {
  MatrixClass TMatrix;
  var         Factor;

  Constructor(DataSetClass data) {
    super.Constructor(data);
    MaxDigits = 4;

    var count = data.UniqueSegments.Length;
    TMatrix = new MatrixClass(count, count, null);
    var tmax = 1;

    foreach (var cognate in data.UsedCognates) {
      var length = cognate.Words.Length;
      for (var i = 0; i < length; ++i) {
        var pi = cognate.Words[i].Padded;
        for (var j = 0; j < length; ++j) {
          if (i != j) {
            var pj = cognate.Words[j].Padded;
            var ml = Min(pi.Length, pj.Length);
            for (var index = 0; index < ml; ++index) {
              var si = pi[index];
              var sj = pj[index];
              if (si != sj and not si.Punctuation and not sj.Punctuation) {
                var list = TMatrix.GetValue(si.Index, sj.Index);
                var p    = new WordPairClass(index, cognate.Words[i], cognate.Words[j]);
                if (list is null) {
                  list = new ListClass<WordPairClass>(128);
                  TMatrix.SetValue(si.Index, sj.Index, list);
                }
                list.Add(p);
                if (list.Length > tmax)
                  tmax = list.Length;
              }
            }
          }
        }
      }
    }
    Factor = 1.0 / tmax;
  }

  override AddEntry(SegPartClass si, SegPartClass sj, x, y, SizeClass size) {
    var value = TMatrix.GetValue(si.Index, sj.Index);
    if (value is null)
      return null;

    return Canvas(size.Width, size.Height) {
      X: x;
      Y: y;
      HAlign: HAligns.Center;
      VAlign: VAligns.Center;
      TipAction: new TranActionClass(value.Values, value.Length*Factor);

      var count = 0;
      foreach (var e in value) {
        var c0 = e.Word1.Padded[e.Index];
        var c1 = e.Word2.Padded[e.Index];
        if (c0.Features.HasFlag(VFlags) != c1.Features.HasFlag(VFlags))
          ++count;
      }

      Background: count > 0 ? Greenish(count / value.Length) : Redish(value.Length * Factor);
      if (value.Length > 0)
        value.Length;
    };
  }
}

//======================================================================

class RateMatrixClass {
  MatrixClass Values;
  bool        Symmetric;
  var         MaxValue;

  Constructor(rates, bool symmetric) {
    Symmetric = symmetric;
    Values    = Math.Matrix(rates);
    MaxValue  = Values.FindMax;
  }
}

//======================================================================


class AlignedCountClass: CountBaseClass {
  ListClass<AlignActionClass>   ActionList;
  MatrixClass<AlignActionClass> ActionMatrix;
  ResultsClass                  Results;
  var                           Factor;
  AlignActionClass[]            SortedActions;

  Constructor(ResultsClass results, RateMatrixClass rates) {
    super.Constructor(results.Data);
    Results = results;

    var ordered = results.Data.UniqueSegments;
    var length  = Segments.Length;

    System.Assert(length == rates.Values.Rows);
    System.Assert(length == rates.Values.Cols);
    System.Assert(length == ordered.Length);


    ActionMatrix = new(length, length, null);
    ActionList   = new(length * length);
    var values   = rates.Values;

    for (var i = 0; i < length; ++i) {
      var jmax = rates.Symmetric ? i : length;

      for (var j = 0; j < jmax; ++j) {
        var rate = values.GetValue(i, j);
        if (rates.Symmetric) {
          var a = new AlignActionClass(results, ordered[i], ordered[j], rate, rate, rates.Symmetric);
          ActionList.Add(a);
          ActionMatrix.SetValue(i, j, a);
        }
        else {
          var reverse = values.GetValue(j, i);
          var a       = new AlignActionClass(results, ordered[i], ordered[j], rate, reverse, rates.Symmetric);
          ActionList.Add(a);
          ActionMatrix.SetValue(i, j, a);
        }
      }
    }

    var f = 1.0 / rates.MaxValue;
    foreach (var a in ActionList) {
      a.Percent  = a.Rate * f;
      a.RPercent = a.Reverse * f;
    }

    Factor        = f;
    SortedActions = ActionList.Sort(true);
  }

  ShowLegend(AlignActionClass[] actions) = {
    // Show only partitions that are present in the chart
    var pmax = Results.Rule.Partitions.Length;
    var set  = new HashSetClass(pmax);
    var list = new ListClass<PartitionClass>(pmax);
    foreach (var s in actions) {
      var i = s.S0.Partition;
      if (set.Add(i))
        list.Add(i);
      i = s.S1.Partition;
      if (set.Add(i))
        list.Add(i);
    }

    Canvas(ExtentWidth) {
      HAlign: HAligns.Center;
      Results.Data.Rule.ShowBlockLegend(list.Values, 11 pts);
    }
  };

  ShowChart(AlignActionClass[] actions) = Chart(100%, 4 inches) {
    Type: ChartTypes.Column;
    TextHeight: 12 pts;
    ValueLabel: HBox {
      Transform: Rotate(90 degrees);
      TransformFit: true;
      Lang.RateChange;
    };

    var fs = 200.0 / actions.Length;
    XAxis: ChartAxis {
      Rotation: 30 degrees;
      (each actions).ShowBox(fs);
    };

    ChartSeries(Colors.Green) {
      Label: Tex.rightarrow;
      foreach (var a in actions)
        new DataPointClass(each0, a.Rate) {Reference: a};
    };
  };

  Extrema(number, top) = VBox {
    var actions = SortedActions.Slice(top ? 0..<number : (SortedActions.Length - number)..<SortedActions.Length);
    ShowChart(actions);
    Separation: 20 pts;
    ShowLegend(actions);
  };


  MinusLog(text) = Span {
    Tex.Minus; "Log" sub 10; "("; text; ")";
  };

  LogRate(r) {
    if (r < 0)
      return Math.Log(-r, 10);
    else
      return -Math.Log(r, 10);
  }

  ShowAllRates = VBox {
    Chart(100%, ExtentHeight - 48 pts) {
      Type: ChartTypes.Scatter;
      TextHeight: 12 pts;

      ValueLabel: HBox {
        Transform: Rotate(90 degrees);
        TransformFit: true;
        MinusLog(Lang.RateChange);
      };

      XAxis: ChartAxis {
        Rotation: 90 degrees;
        TextHeight: 7 pts;
        foreach (var sa in Results.Rule.Segments) {
          var p = Results.Rule.Partitions[sa.PartitionIndex];
          HBox(15 pts) {
            HAlign: HAligns.Center;
            Background: p.Color;
            TextColor: p.TextColor;
            sa.Segment.Ipa;
          }
        }
      };

      var special = Markers.Circle(Colors.Red);

      ChartSeries {
        Marker: Markers.Dot;

        foreach (var si in Results.Rule.Segments) {
          var i = each0;

          foreach (var sj in Results.Rule.Segments) {
            if (si.Index != sj.Index) {
              var a = ActionMatrix.GetValue(si.Index, sj.Index);
              var y = LogRate(a.Rate);

              new DataPointClass(i, y) {
                Reference: a;
                Fill: a.S1.Partition.Color;
              };
            }
          }
        }
      };
    };

    Separation: 20 pts;
    Canvas(ExtentWidth) {
      HAlign: HAligns.Center;
      Results.Data.Rule.ShowBlockLegend(Results.Rule.Partitions, 10 pts);
    }
  };

  ShowSortedRates = VBox {
    Chart(100%, ExtentHeight - 48 pts) {
      Type: ChartTypes.Scatter;
      TextHeight: 12 pts;

      XLabel: MinusLog(Lang.RateChange);

      ValueLabel: HBox {
        Transform: Rotate(90 degrees);
        TransformFit: true;
        MinusLog("Reverse Rate");
      };

      var group   = Markers.TriangleUp(Colors.Green) {TextHeight: 12 pts};
      var special = Markers.Square(Colors.Red) {TextHeight: 15 pts};

      ChartSeries {
        Marker: Markers.Dot;

        foreach (var si in Results.Rule.Segments) {
          foreach (var sj in Results.Rule.Segments) {
            if (si.Index < sj.Index) {
              var a = ActionMatrix.GetValue(si.Index, sj.Index);
              new DataPointClass(LogRate(a.Rate), LogRate(a.Reverse)) {
                Reference: a;
                if (a.S0.Segment.Ipa == "p")
                  Marker: a.S1.Segment.Ipa == "b" or a.S1.Segment.Ipa == "f" ? special : group;
                else
                  Marker: a.S1.Partition.Marker;
              };
            }
          }
        }
      };
    };

    Separation: 20 pts;
    Canvas(ExtentWidth) {
      HAlign: HAligns.Center;
      Results.Data.Rule.ShowBlockLegend(Results.Rule.Partitions, 10 pts);
    }
  };

  ShowChartPairs(top, subset) = VBox {
    var actions = subset.Slice(0..<top);

    ShowChart(actions) {
      Type: ChartTypes.StackedColumn;

      Legend: ChartLegend {
        Vertical: true;
        Placement: Placements.Right;
      };
      ChartSeries(Colors.Red) {
        Label: Tex.leftarrow;
        foreach (var a in actions)
          new DataPointClass(each0, a.Reverse);
      };
    };
    Separation: 20 pts;
    ShowLegend(actions);
  };

  Pairs(top, predicate) = ShowChartPairs(top, SortedActions.FindSlice(predicate));

  override AddEntry(SegPartClass si, SegPartClass sj, x, y, SizeClass size) {
    var a = ActionMatrix.GetValue(si.Index, sj.Index);
    if (a is valid) {
      return Canvas(size.Width, size.Height) {
        X: x;
        Y: y;
        HAlign: HAligns.Center;
        VAlign: VAligns.Center;
        TipAction: a;

        Background: Redish(a.Percent);
//        if (a.Percent >= 0.30)
  //        Span {a.Percent * 100.0; TextDigits: 0};
      };
    }
    return null;
  }
}

//======================================================================

class AsymmetriesClass: AlignedCountClass {
  Constructor(ResultsClass results, RateMatrixClass rates) {
    super.Constructor(results, rates);
  }

  override AddEntry(SegPartClass si, SegPartClass sj, x, y, SizeClass size) {
    var a = ActionMatrix.GetValue(si.Index, sj.Index);
    if (a is valid) {
      return Canvas(size.Width, size.Height) {
        X: x;
        Y: y;
        HAlign: HAligns.Center;
        VAlign: VAligns.Center;
        TipAction: a;

        var r = (a.Rate - a.Reverse) * Factor * 0.5;
        Background: r > 0 ? Greenish(r) : Redish(-r);
//        if (a.Percent >= 0.30)
//          Span {a.Percent * 100.0; TextDigits: 0};
      };
    }
    return null;
  }
}

//======================================================================

class SegDataClass {
  PartitionClass Partition;
  SegPartClass   Segment;
  var            Value;

  Constructor(ResultsClass results, RateMatrixClass rates, i) {
    Segment   = results.Data.UniqueSegments[i];
    Partition = Segment.Partition;
    Value     = Math.Abs(rates.Values.GetValue(i, i));
  }

  override GetLayoutTip = VBox {
    HAlign: HAligns.Center;

    Span {
      "Rate: ";
      TextDigits: 3;
      Value;
    };
    VBox(DocMetrics.SegmentWidth) {
      HAlign: HAligns.Center;
      Background: Partition.Color;
      HBox {
        Partition.Name;
      };
    };
    Segment.GetLayoutTip;
  };
}

//======================================================================

class DiagonalClass {
  SegDataClass[]   Data;
  SegPartClass[]   Segments;
  PartitionClass[] Partitions;
  ResultsClass     Results;

  Constructor(ResultsClass results, RateMatrixClass rates) {
    Results = results;
    var data = new SegDataClass(results, rates, each 0..<rates.Values.Cols);
    Data     = data.Sort(true, ref CompareData);
    Segments = (each Data).Segment;

    var partitions = results.Rule.Partitions;
    var pmax       = partitions.Length;
    var set        = new HashSetClass(pmax);
    var list       = new ListClass<PartitionClass>(pmax);
    foreach (var s in Segments) {
      var i = s.PartitionIndex;
      if (set.Add(i))
        list.Add(partitions[i]);
    }
    Partitions = list.Values;
  }

  CompareData(SegDataClass d0, SegDataClass d1, options) = d0.Value.Compare(d1.Value);

  Display = VBox {
    HAlign: HAligns.Center;
    Style.SansSerif;

    Chart(ExtentWidth, 4 inches) {
      TextHeight: 11 pts;
      Type: ChartTypes.Column;

      ValueLabel: Lang.RateChange {
        Transform: Rotate(90 degrees);
        TransformFit: true;
      };

      XAxis: ChartAxis {
        TextHeight: DocMetrics.SegmentTextFactor * ExtentWidth / Data.Length;
        (each Segments).ShowSegment;
      };

      ValueAxis: ChartAxis;
      ChartSeries {
        foreach (var d in Data) {
          new DataPointClass(each0, d.Value) {
            Reference: d;
            Fill: d.Segment.Partition.Color;
          };
        }
      };
    };

    Results.Data.Rule.ShowBlockLegend(Partitions, 9 pts);
  };
}

//======================================================================

class FrequencyClass {
  var Frequency,
      Index;

  Constructor(frequency) {
    Index     = each0;
    Frequency = frequency;
  }
}

//======================================================================

class RateDataClass {
  TaxonClass       Taxon;
  VectorClass      Count;
  FrequencyClass[] Frequency;
  var              TotalCount,
                   MaxFrequency,
                   Distance;

  Constructor(count, TaxonClass taxon) {
    Taxon        = taxon;
    TotalCount   = 0;
    MaxFrequency = 0;
    Count        = new VectorClass(count, 0);
  }
}

//======================================================================

class SegmentActionClass: ActionClass {
  RateDataClass Rate;
  SegPartClass  Segment;
  TaxonClass    Taxon;
  var           Count,
                Percent;

  Constructor(RateDataClass rate, SegPartClass segment, count, percent) {
    super.Constructor(Actions.RunQuery, null);
    Segment = segment;
    Taxon   = rate.Taxon;
    Count   = count;
    Percent = percent;
  }

  override GetQuery = MatchAll WordClass {
    MatchAny LanguageClass {
      Taxon.Language;
    };
    MatchAny SegmentClass {
      Segment;
    };
  };

  override GetLayoutTip = VBox {
    HAlign: HAligns.Center;
    Span {
      Lang.Occurrences(Count);
      Space;
      Span {
        "(";
        TextDigits: 1;
        Percent * 100.0;
        "%)";
      }
    };
    Segment.GetLayoutTip;
  };
}

//======================================================================

abstract class FrequencyChartClass {
  PartitionClass[] Partitions;
  RuleClass        Rule;
  TaxonClass[]     Taxa;
  VectorClass      UsedCount,
                   UsedFrequency;
  FrequencyClass[] FirstRate;
  RateDataClass[]  Rates,
                   SortedRates;
  var              RateCount,
                   SortedIndexes,
                   MaxDistance;

 Constructor(DataSetClass data, count) {
    Partitions = data.Rule.Partitions;
    Rule       = data.Rule;
    Taxa       = data.Taxa;
    RateCount  = count;
    Rates      = new RateDataClass(count, each Taxa);
  }

  CompareFrequency(i0, i1, options) {
    var f0 = FirstRate[i0];
    var f1 = FirstRate[i1];
    var d  = f0.Frequency.Compare(f1.Frequency);
    if (d == 0)
      return f0.Index - f1.Index;
    return d;
  }

  static CompareDistance(RateDataClass r0, RateDataClass r1, options) {
    return r0.Distance.Compare(r1.Distance);
  }

  void ComputeDistance(sortfunction) {
    // Compute the Euclidean distance between each language and the first language

    var fr = new VectorClass(RateCount, 0.0);
    foreach (var rate in Rates) {
      var mult = 100.0 / rate.TotalCount;
      for (var i = 0; i < RateCount; ++i) {
        var f = mult * rate.Count[i];
        fr.SetValue(i, f);
        if (f > rate.MaxFrequency)
          rate.MaxFrequency = f;
      }
      rate.Frequency = new FrequencyClass(each fr);
    }

    var max = 0.0;
    var r0  = Rates[0];
    foreach (var rate in Rates) {
      if (each0 > 0) {
        var sum = 0;
        for (var i = 0; i < RateCount; ++i) {
          var delta = rate.Frequency[i].Frequency - r0.Frequency[i].Frequency;
          sum += delta * delta;
        }
        rate.Distance = Math.Sqrt(sum);
        if (rate.Distance > max)
          max = rate.Distance;
      }
      else
        rate.Distance = 0.0;
    }

    MaxDistance = max;
    FirstRate   = r0.Frequency;
    var a = 0 ..< RateCount step 1;
    SortedIndexes = a.Sort(true, sortfunction);
    SortedRates   = Rates.Sort(false, ref CompareDistance);
  }

  abstract SegmentBar(RateDataClass rate, index, width, height);
  abstract OccurranceChart(SizeClass size);

  LangCanvas(RateDataClass rate, width, height) = HBox(null, height) {
    var cborder = 0.25 pts;

    VAlign: VAligns.Bottom;
    Border: BorderB(cborder);
    SegmentBar(rate, each SortedIndexes, width, height - cborder);
  };

  ShowChart = HBox {
    VAlign: VAligns.Center;
    Rule.ShowLegend(10 pts, 1.4 inch, 5) {
      Border: new EdgeClass(0.25 pts, 80%);
      Margin: PadR(12 pts);
    };
    OccurranceChart(new(ExtentWidth - 2 inches, ExtentHeight));
  };
}
//======================================================================

class SegmentFrequencyClass: FrequencyChartClass {
  VectorClass    PartitionMax;
  SegPartClass[] Segments;

  Constructor(DataSetClass data, group) {
    super.Constructor(data, data.UniqueSegments.Length);

    Segments     = data.UniqueSegments;
    PartitionMax = new(data.Rule.Partitions.Length, 0);

    foreach (var cognate in data.UsedCognates) {
      foreach (var word in cognate.Words) {
        var rate = Rates[word.Taxon.Index];
        foreach (var segment in word.Segments) {
          ++rate.TotalCount;
          rate.Count.AddValue(segment.Index, 1);
          PartitionMax.AddValue(segment.PartitionIndex, 1);
        }
      }
    }

    ComputeDistance(group ? ref CompareGroups : ref CompareFrequency);
  }

  CompareGroups(i0, i1, options) {
    var f0 = FirstRate[i0];
    var f1 = FirstRate[i1];
    var d  = PartitionMax[Segments[f0.Index].PartitionIndex] - PartitionMax[Segments[f1.Index].PartitionIndex];
    if (d == 0) {
      d = f0.Frequency.Compare(f1.Frequency);
      if (d == 0)
        return f0.Index - f1.Index;
    }
    return d;
  }

  override SegmentBar(RateDataClass rate, index, width, height) {
    var f = rate.Frequency[index].Frequency / rate.MaxFrequency;
    return Canvas(width-1, height * f) {
      Margin: PadR(1);
      var s = Segments[index];
      Background: s.Partition.Color;
      TipAction: new SegmentActionClass(rate, s, rate.Count[index], f);
    };
  }

  SegmentLabel(SegPartClass[] segments, index, width, height) = HBox(width, height) {
    HAlign: HAligns.Center;
    segments[index].ShowSegment;
  };

  override OccurranceChart(SizeClass size) = MatrixBlock {
    var lw     = 70 pts;
    var cw     = size.Width - lw;
    var bw     = cw / Segments.Length;
    var bh     = bw * 1.5;
    var h      = (size.Height - bh) / Taxa.Length;
    var r0     = SortedRates[0];
    var factor = 1.0 / MaxDistance;

    TextHeight: 12 pts;
    VAlign: VAligns.Bottom;
    foreach (var rate in SortedRates) {
      Row {
        Cell(PadR(3 pts)) {
          Frame(lw) {
            rate.Taxon.Language;
          };
          if (rate.Distance > 0) {
            var frac = factor * rate.Distance;
            Canvas(lw * frac, 2) {
              Background: Colors.Red;
              Tip: new TipClass("{0}% different from {1}"(frac*100.0 {TextDigits: 1}, r0.Taxon.Language));
            };
          }
        };
        LangCanvas(rate, bw, h);
      };
    }

    Row {
      Empty;
      TextHeight: 1.1 * bw;
      HBox {
        SegmentLabel(Segments, each SortedIndexes, bw, bh);
      };
    }
  };
}

//======================================================================

class PartitionRateActionClass: ActionClass {
  RateDataClass  Rate;
  PartitionClass Partition;
  TaxonClass     Taxon;
  var            Count,
                 Percent;

  Constructor(RateDataClass rate, PartitionClass partition, count, percent) {
    super.Constructor(Actions.RunQuery, null);
    Partition = partition;
    Taxon     = rate.Taxon;
    Count     = count;
    Percent   = percent;
  }

  override GetQuery = MatchAll WordClass {
    MatchAny LanguageClass {
      Taxon.Language;
    };
    MatchAny SegmentClass {
      Partition.Segments;
    };
  };

  override GetLayoutTip = VBox {
    HAlign: HAligns.Center;
    Span {
      Lang.Occurrences(Count);
      Space;
      Span {
        "(";
        TextDigits: 1;
        Percent * 100.0;
        "%)";
      }
    };
  };
}

//======================================================================

class PartitionFrequencyClass: FrequencyChartClass {
  Constructor(DataSetClass data) {
    super.Constructor(data, data.Rule.Partitions.Length);

    foreach (var cognate in data.UsedCognates) {
      foreach (var word in cognate.Words) {
        var rate = Rates[word.Taxon.Index];
        foreach (var segment in word.Segments) {
          ++rate.TotalCount;
          rate.Count.AddValue(segment.PartitionIndex, 1);
        }
      }
    }

    ComputeDistance(ref CompareFrequency);
  }

  override SegmentBar(RateDataClass rate, index, width, height) {
    var f = rate.Frequency[index].Frequency / rate.MaxFrequency;
    return Canvas(width-1, height * f) {
      Margin: PadR(1);
      var partition = Partitions[index];
      Background: partition.Color;
      TipAction: new PartitionRateActionClass(rate, partition, rate.Count[index], f);
    };
  }

  PartitionLabel(index, width, height) = Frame(width, height) {
    ParAlignment: ParAlignments.Center;
    Partitions[index].Name;
  };

  override OccurranceChart(SizeClass size) = MatrixBlock {
    var lw = 60 pts;
    var cw = size.Width - lw;
    var bw = cw / Partitions.Length;
    var bh = 16 pts;
    var h  = (size.Height - bh) / Taxa.Length;
    var r0 = SortedRates[0];

    TextHeight: 11 pts;
    VAlign: VAligns.Bottom;

    var factor = 1.0 / MaxDistance;
    foreach (var rate in SortedRates) {
      Row {
        Cell(PadR(3 pts)) {
          Frame(lw) {
            rate.Taxon.Language;
          };

          var frac = factor * rate.Distance;
          Canvas(lw * frac, 2) {
            Background: Colors.Red;
            Tip: new TipClass("{0}% different from {1}"(frac*100.0 {TextDigits: 1}, r0.Taxon.Language));
          };
        };
        LangCanvas(rate, bw, h);
      };
    }

    Row {
      Empty;
      TextHeight: 9 pts;
      HBox {
        VAlign: VAligns.Top;
        PartitionLabel(each SortedIndexes, bw, bh);
      };
    }
  };
}

//======================================================================

