using Format, IO, Units, Languages;

//======================================================================

class RunExperimentClass {
  static OutFileName = Info.ExecuteFolder Folder("out");

  var        Name,
             RootFolder,
             Generations,
             PreBurn,
             Print,
             Seed,
             Tree,
             Complete,
             Marginal;
   ModeClass Mode;

  Constructor(ModeClass mode, name, generations, preburn, print, marginal) {
    Name        = name;
    Mode        = mode;
    Generations = generations;
    PreBurn     = preburn;
    Print       = print;
    Marginal    = marginal;
    RootFolder  = mode.RootFolder Folder(name);
    Seed        = Math.Random(0..0xFFFFFFFF);

    var treefile = Folders.Repository RootFolder Info.ExecuteFolder FileName("consensus.tre");
    Complete = FileExists(treefile);
    if (Complete)
      Tree = Read(treefile).trees.TREE1;
  }

  ArgString(arg, text) = Span {
    var path = String(text);
    if (valid arg) {
      "-";
      arg;
      Space;
    }
    var quote = path.FindFirstIndex(c => c == ' ') >= 0;
    if (quote)
      Quote;
    path;
    if (quote)
      Quote;
  };

  Pause(mac) = mac ? "read -rsp $'Press enter to continue...\\n'" : "pause";

  ArgSpan = Span {
    Separator: Space;
  };

  RunMCMC(mac, root) = TextBlock {
    ArgSpan {
      ArgString(null, mac ? Info.MacMCMCEXE : Info.MCMCEXE);
      ArgString("d", Info.ConfigFile);
      ArgString("o", root OutFileName);
    };
    Pause(mac);
  };

  RunReader(mac, root) = TextBlock {
    ArgSpan {
      ArgString(null, mac ? Info.MacReaderEXE : Info.ReaderEXE);
      ArgString("i", root Info.ExecuteFolder);
      ArgString("o", root OutFileName);
    };
    Pause(mac);
  };

  RunAll(mac, root) = TextBlock {
    RunMCMC(mac, root);
    RunReader(mac, root);
  };

  void WriteRuns(path, mac, root, reader, all) {
    Write(RunReader(mac, root), path FileName(reader), FileFormats.UTF8);
    Write(RunAll(mac, root), path FileName(all), FileFormats.UTF8);
  }

  void InitFolders(ExperimentClass experiment) {
    var path = Folders.Repository RootFolder;

    CreateFolder(path);
    CreateFolder(path Info.ExecuteFolder);
    WriteRuns(path, false, path, "WinRunReader.bat", "WinRunAll.bat");
    WriteRuns(path, true, Empty, "MacRunReader.sh", "MacRunAll.sh");

    Write(new ConfigFileClass(experiment, this).Show, path Info.ConfigFile);
  }
}

//======================================================================

class ModeClass {
  var                  Name,
                       RootFolder;
  ModelFolderClass     Model;
  RunExperimentClass[] Run;

  Constructor(ModelFolderClass model, name, generations, preburn, print, marginal) {
    Name       = model.Name + "-" + name;
    Model      = model;
    RootFolder = model.RootFolder Folder(name);
    Run        = new RunExperimentClass(this, each ["Run1", "Run2"], generations, preburn, print, marginal);
  }

  void InitFolders(ExperimentClass experiment) {
    CreateFolder(Folders.Repository RootFolder);
    (each Run).InitFolders(experiment);
  }
}

//======================================================================

class ModelFolderClass {
  var       Name,
            RootFolder;
  ModeClass Posterior,
            Marginal;

  Constructor(ExperimentClass experiment, folder, name=null) {
    Name       = name ?? folder;
    RootFolder = experiment.RootFolder Folder(folder);
    Posterior  = new ModeClass(this, "Posterior", 1_000_000, 20_000, 1000, false);
    Marginal   = new ModeClass(this, "Marginal", 50_000, 5000, 100, true);
  }

  void InitFolders(ExperimentClass experiment) {
    CreateFolder(Folders.Repository RootFolder);
    Posterior.InitFolders(experiment);
    Marginal.InitFolders(experiment);
  }
}

//======================================================================

class RunInitClass: RunClass {
  var Experiment;

  Constructor(ExperimentClass experiment) {
    super.Constructor(experiment.FactId, "Create \"{0}\" data files"(experiment.Name));
    Experiment = experiment;
  }

  override void Execute {
    Experiment.InitFolders;
  }

  override AskQuestion = "Overwrite configuration files for experiment \"{0}\"?"(Name);
}

//======================================================================

class NLeaf: NodeClass {
  Constructor(DataSetClass data, LanguageClass language, branch) {
    super.Constructor("{0} ({1})"(language, data.LangNumber(language)));
    BranchLength = branch;
  }
}

class NBranch: NodeClass {
  Constructor(name, branch=1) {
    super.Constructor(name);
    BranchLength = branch;
  }
}

//======================================================================

abstract class ExperimentClass: FactClass {
  field               Name,
                      FolderName;
  RuleClass           Rule;
  DataSetClass        Data;
  ModelFolderClass    Custom,
                      JC69,
                      GTR;
  ModelFolderClass[]  Models;
  ExampleConceptClass ExampleConcept,
                      DataConcept;
  CalcRatesClass      CalcExperiment;
  var                 RootFolder,
                      MacFolder;

  Constructor(id, name, folder, RuleClass rule) {
    super.Constructor(id);
    Rule           = rule;
    Data           = rule.Data;
    Name           = name;
    FolderName     = folder;
    RootFolder     = Folder("Experiments") Folder(folder);
    ExampleConcept = new ExampleConceptClass(Data, Trial.ExampleConcept);
    DataConcept    = new ExampleConceptClass(Data, Trial.DataConcept);
    CalcExperiment = new CalcRatesClass(Data, Rule);
    Custom         = new ModelFolderClass(this, "Custom", rule.Name);
    JC69           = new ModelFolderClass(this, "JC69");
    GTR            = new ModelFolderClass(this, "GTR");
    Models = [
      Custom,
      JC69,
      GTR,
    ];
  }

  override IconClass GetIcon = Icons.science;

  abstract NodeClass NumberedTree;
  abstract NodeClass PriorTree;

  void InitFolders {
    if (not FolderExists(RootFolder)) {
      CreateFolder(Folders.Repository RootFolder);
      (each Models).InitFolders(this);
    }
  }

  MainRun = Custom.Posterior.Run[0];

  GetDocuments = [
    new SubmissionViewClass(this),
    new SupplimentalViewClass(this),
    new ExperimentViewClass(this),
    new DiagnosticsViewClass(this),
    new ConfigurationViewClass(this),
    new AlignmentViewClass(this),
    new DavidLinguisticPresentationClass(this),
//    new DavidBioPresentationClass(this),
    new JohnPresentationClass(this),
  ];

  CalculateLoad(SegmentClass[] segments, sum) {
    return sum;
  }

  ShowFunctionalLoad = Block {
    Style.Header1 {
      "Functional Load of \"{0}\" model"(Rule.Name);
    };

    Table {
      Columns: [1.5 inch, 1 inch {HAlign: HAligns.Right}];
      Row {
        Lang.Partition;
        Lang.FrequencyPercent;
        Lang.FunctionalLoad;
      };

      foreach (var part in Rule.Partitions) {
        var sum = 0;//Math.Sum((each part.Segments).UsedFrequency);
        Row {
          part.Name;
          Cell {
            TextDigits: 2;
            sum;
          };
          Cell {
            TextDigits: 2;
            CalculateLoad(part.Segments, sum);
          }
        };
      }
    }
  };

  ShowCognateCoverage = Block {
    Style.Header1 {
      "By Language";
    };
    Chart {
      Type: ChartTypes.Column;
      TextHeight: 10 pts;
      Size: new SizeClass(Extent.Size.Width, 3 inches);
      ValueLabel: "%";

      XAxis: ChartAxis {
        (each Data.Taxa).Language;
      };
      ValueAxis: ChartAxis;

      ChartSeries {
        foreach (var t in Data.Taxa)
          Data.Coverage(t) * 100.0;
      };
    };
    Paragraph;
    Style.Header1 {
      "By Number of Languages";
    };

    var coverage = Data.CoverageByLanguages;
    Chart {
      Type: ChartTypes.Column;
      TextHeight: 10 pts;
      Size: new SizeClass(Extent.Size.Width, 3 inches);

      ValueLabel: Lang.Cognates {
        Transform: Rotate(90 degrees);
        TransformFit: true;
      };
      ValueAxis: ChartAxis;

      XAxis: ChartAxis {
        0..<coverage.Length step 1;
      };
      XLabel: "Languages Included";

      ChartSeries {
        foreach (var c in coverage)
          c;
      };
    };
  };

  ShowTrees(ModelFolderClass folder) = Block {
    var any = false;
    foreach (var r in folder.Posterior.Run) {
      if (valid r.Tree) {
        any = true;
        break;
      }
    }

    if (any) {
      Style.Header1 {
        Lang.Model;
        ColonSpace;
        folder.Name;
      };

      foreach (var r in folder.Posterior.Run) {
        if (valid r.Tree)
          Style.ShowTree("Run {0}"(each1), null, r.Tree, null);
      }
    }
  };


  ShowExperiment = Block {
    Style.Header1 {
      Lang.Experiment;
      Space;
      each1;
      Space;
      Style.DQuote {Name};
    };

    Style.ShowTree("Prior", null, PriorTree, null);
    ShowTrees(Custom);
    ShowTrees(JC69);
    ShowTrees(GTR);
  };

  NodeClass GetNumberedTree(portuguese) = new NBranch(19) {
    new NLeaf(Data, Latin, 7);
    new NBranch(18) {
      new NLeaf(Data, Romanian, 6);
      new NBranch(17) {
        new NBranch(16) {
          new NBranch(15) {
            new NLeaf(Data, Walloon, 3);
            new NLeaf(Data, French, 3);
          };
          new NBranch(14) {
            new NBranch(13) {
              portuguese;
              new NLeaf(Data, Spanish, 2);
            };
            new NLeaf(Data, Catalan, 3);
          }
        };
        new NBranch(11) {
          new NLeaf(Data, Friulian, 4);
          new NLeaf(Data, Italian, 4);
        };
      }
    }
  };
}
//======================================================================


