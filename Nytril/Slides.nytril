using Format, Units, Math, IO;

//======================================================================

with Slides.Questions {
  Title = Lang.Questions;

  Body = Paragraph {
    ParAlignment: ParAlignments.Center;
    SpaceBefore: 1 inch;
    Figure {
      TextHeight: 150 pts;
      Fill: (Style.LightColor..Style.DarkColor) {Vertical: true};
      "?";
    }
  };
}

with Slides.Introduction {
  Title = PaperSections.Introduction.Title;
  Body  = Block {
    PaperSections.Introduction.Slide;
    Style.ShowTable(TableFigures.Cognates.Data);
  };
}

with Slides.ExampleTree {
  Title = "Example Tree";
  Body  = Figures.ExampleTree.Data.ShowSlide;
}

with Slides.Alignment {
  Title = Lang.Alignment;
  Body  = Figures.Alignment.Data.ShowSlide;
}

with Slides.ShowResults {
  Title = "Results";
  Body  = ReadTree(Lang.MCC, Results.OutFolder Results.GammaMCC);
}

with Slides.LanguagesUsed {
  Title = Lang.Languages;
  Body  = Results.ShowLanguages;
}

with Slides.ConceptsUsed {
  Title = Lang.ConceptsUsed;
  Body  = AllWords.ShowTable;
}

with Slides.CharacterRules {
  Title = Lang.CharacterRules;
  Body  = Results.CharacterRule.ShowDescription;
}

with Slides.PartitionRules {
  Title = Lang.PartitionRules;
  Body  = Results.PartitionRule.ShowPartitions;
}

with Slides.SegmentFrequencies {
  Title = Lang.SegmentFrequencies;
  Body  = Results.FrequencyChart(Size(DocMetrics.SlideContent.Width, DocMetrics.SlideContent.Height - 180 pts));
}

with Slides.PartitionDiagram {
  Title = Lang.TransitionRates;
  Body  = Block {
    Figures.FrequencyDiagram.Caption;
    new GroupRatesClass(Results).PartitionDiagram(DocMetrics.SlideContent);
  };
}
//======================================================================

class PresentationViewClass: BaseViewClass {
  var SlideList,
      Author;

  Constructor(id, title, author) {
    super.Constructor(id, title);
    DocumentType = ConfigurationClass.DocumentTypes.SlideShow;
    Author       = author;
    SlideList    = new ListClass(64);
  }

  void AddSlide(slide) {
    SlideList.Add(slide);
  }

  override GetDocument = Style.MainDocument {
    Block {
      LeftIndent: 1 inch;

      Paragraph {
        TextHeight: 40 pts;
        SpaceBefore: 1.5 inches;
        Title;
      };
      Paragraph {
        TextHeight: 18 pts;
        SpaceBefore: 1.5 inches;
        Author.FirstName;
        Space;
        Author.LastName;
      };
      var department = Author.FindFirstFact(LinkTypes.WorkDepartment);
      if (valid department) {
        Paragraph {
          TextHeight: 14 pts;
          department.Name;
        };
        var org = department.FindFirstFact(LinkTypes.Department);
        if (valid org) {
          Paragraph {
            TextHeight: 14 pts;
            org.Name;
          };
        }
      }
      Paragraph {
        TextHeight: 12 pts;
        var email = Author.FindFirstFact(LinkTypes.WorkEmail);
        if (valid email)
          email.Value;
      };
    };

    foreach (var slide in SlideList)
      AddSlideBlock(slide, EachOne);
  };
}
//======================================================================

class JohnPresentationClass: PresentationViewClass {
  Constructor {
    super.Constructor(#e8e882037e4b3a438133df5adc2eb7b6#, "John's Presentation", People.JPHuelsenbeck);
    AddSlide(Slides.Introduction);
    AddSlide(Slides.LanguagesUsed);
    AddSlide(Slides.ExampleTree);
    AddSlide(Slides.Alignment);
    AddSlide(Slides.CharacterRules);
    AddSlide(Slides.PartitionRules);
    AddSlide(Slides.SegmentFrequencies);
    AddSlide(Slides.PartitionDiagram);
    AddSlide(Slides.ShowResults);
    AddSlide(Slides.Questions);
  }
}
//======================================================================

class DavidPresentationClass: PresentationViewClass {
  Constructor {
    super.Constructor(#a6e65a8a39890e46844d37c5125b2de9#, "David's Presentation", People.DMGoldstein);
    AddSlide(Slides.Introduction);
    AddSlide(Slides.LanguagesUsed);
    AddSlide(Slides.ConceptsUsed);
    AddSlide(Slides.CharacterRules);
    AddSlide(Slides.PartitionRules);
    AddSlide(Slides.SegmentFrequencies);
    AddSlide(Slides.PartitionDiagram);
    AddSlide(Slides.ShowResults);
    AddSlide(Slides.Questions);
  }
}
//======================================================================

