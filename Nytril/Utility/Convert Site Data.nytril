using Format, Units, IO, DB;

//================================================================


Schema = new LogSchemaClass;

class MyDocClass: DocumentClass {
  Constructor {
    super.Constructor(#2f91b3da-1566-4591-bcc7-1ec7b798a87f#, "Convert Site Data");

    var view = Schema.ReadTextFile(IO.Folder("c:/temp") FileName("SoundComparisons Export.txt"), true);
    Schema.GetData(view);
  }

  override GetDocument = Document {
    Schema.ShowDoc;
  };

//void Out {
//  IO.Write(Schema.ShowDoc, Folders.Source Folders.Up IO.Folder("Concepts") FileName("Phonetic.nytril"));
//}
}

//================================================================

Main.Documents.MainDocument = new MyDocClass;

//================================================================

class WordClass {
  var Language,
      Text,
      Spelling;

  Constructor(language, text, spelling) {
    Language = language;
    Text     = text;
    Spelling = spelling;
  }
}

//================================================================

class ConceptClass {
  var Name,
      Words;

  Constructor(name) {
    Name  = name;
    Words = new DictionaryClass<WordClass>(16);
  }
}

//================================================================

class LogSchemaClass: DbSchemaClass {
  DB.DbFieldClass               Language,
                                Concept,
                                Text,
                                Spelling;
  DictionaryClass<ConceptClass> Concepts;

  Constructor {
    super.Constructor("log");
    Concepts = new(1024);
    Language = new("Language", ValueTypes.String);
    Concept  = new("Concept", ValueTypes.String);
    Text     = new("Text", ValueTypes.String);
    Spelling = new("Spelling", ValueTypes.String);
    AddField(Language);
    AddField(Concept);
    AddField(Text);
    AddField(Spelling);
  }

  void GetData(view) {
    foreach (var row in view) {
      var cname = row.FindValue(Concept);
      var concept = Concepts.FindKey(cname);
      if (invalid concept) {
        concept = new ConceptClass(cname);
        Concepts.Add(concept, cname);
      }
      var word = new WordClass(row.FindValue(Language), row.FindValue(Text), row.FindValue(Spelling));
      concept.Words.Add(word, word.Language);
    }
  }

  ShowDoc = TextBlock {
    "using Format, WordGroups;";
    Empty;
    "namespace Concepts;";

    foreach (var concept in Concepts) {
      "//==================================================";
      Empty;

      Span {
        "namespace ";
        ToUpper(concept.Name[0]);
        concept.Name[1..];
        " {";
      };
      "  Confidence = 10;";
      "  WordType   = WordTypes.Noun;";
      "  WordGroup  = Leipzig Swadesh100 Swadesh207;";
      Empty;
      "  namespace Primary {";

      var length = 0;
      foreach (var word in concept.Words)
        length = Math.Max(length, word.Text.Length);

      foreach (var word in concept.Words) {
        Span {
          Space*4;
          word.Language;
          Space * (20 - word.Language.Length);
          "= \"";
          Span {
            TextFamily: TextFamilies.CambriaMath;
            word.Text;
            "\";";
            Space * (length - word.Language.Length + 2);

            if (valid word.Spelling) {
              "  // ";
              word.Spelling;
            }
          }
        }
      }
      "  }";
      "}";
      Empty;
    }
  };
}

//================================================================
